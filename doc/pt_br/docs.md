# V - Documenta√ß√£o

## Introdu√ß√£o

V √© uma linguagem de programa√ß√£o compilada estaticamente tipada projetada
para a constru√ß√£o de software sustent√°vel.


√â semelhante a Go e seu design tamb√©m foi influenciado por Oberon, Rust, Swift,
Kotlin e Python.

V √© uma linguagem muito simples. Analisar esta documenta√ß√£o levar√° cerca de uma hora,
e no final voc√™ ter√° aprendido praticamente toda a linguagem.

A linguagem promove a escrita de c√≥digo simples e claro com abstra√ß√£o m√≠nima.

Apesar de ser simples, V d√° ao desenvolvedor muito poder.
Tudo o que voc√™ pode fazer em outras linguagens, voc√™ pode fazer em V.

## Instala√ß√£o a partir do c√≥digo fonte
A principal forma de obter o melhor e mais recente V √© __instal√°-lo a partir do c√≥digo-fonte__.
√â __f√°cil__ e geralmente leva __apenas alguns segundos__.


### Linux, macOS, FreeBSD, etc:
Voc√™ precisa de `git` e um compilador C como` tcc`, `gcc` ou` clang` e `make`:
```bash
git clone https://github.com/vlang/v
cd v
make
```

### Windows:
Voc√™ precisa de `git` e um compilador C como` tcc`, `gcc`,` clang` ou `msvc`:
```bash
git clone https://github.com/vlang/v
cd v
make.bat -tcc
```
Obs: Voc√™ tamb√©m pode passar um de `-gcc`,` -msvc`, `-clang` para` make.bat` em vez disso,
se voc√™ preferir usar um compilador C diferente, mas -tcc √© pequeno, r√°pido e
f√°cil de instalar (V ir√° baixar um bin√°rio pr√©-constru√≠do automaticamente).

Recomenda-se adicionar esta pasta ao PATH de suas vari√°veis ‚Äã‚Äãde ambiente.
Isso pode ser feito com o comando `v.exe symlink`.

### Android
A execu√ß√£o de aplicativos gr√°ficos V no Android
tamb√©m √© poss√≠vel via [vab](https://github.com/vlang/vab).

Depend√™ncias do V no Android: **V**, **Java JDK** >= 8, Android **SDK + NDK**.

  1. Para instalar as depend√™ncias (veja [vab](https://github.com/vlang/vab))
  2. Conecte seu dispositivo Android
  3. Rode o comando:
  ```bash
  git clone https://github.com/vlang/vab && cd vab && v vab.v
  ./vab --device auto run /path/to/v/examples/sokol/particles
  ```
Para mais detalhes e solu√ß√£o de problemas,
visite o [reposit√≥rio vab GitHub](https://github.com/vlang/vab).

## √çndice

<table>
    <tr><td width=33% valign=top>

* [Hello world](#hello-world)
* [Executando uma pasta de projeto](#executando-uma-pasta-de-projeto-com-v√°rios-arquivos)
* [Coment√°rios](#coment√°rios)
* [Fun√ß√µes](#fun√ß√µes)
    * [Retornando v√°rios valores](#retornando-v√°rios-valores)    
* [Visibilidade de S√≠mbolo](#visibilidade-de-s√≠mbolo)
* [Vari√°veis](#vari√°veis)
* [Tipos](#tipos)
    * [Strings](#strings)
    * [N√∫meros](#n√∫meros)    
    * [Arrays](#arrays)
    * [Arrays de tamanho fixo](#arrays-de-tamanho-fixo)    
    * [Maps](#maps)
* [Importa√ß√£o de m√≥dulos](#importa√ß√£o-de-m√≥dulos)
* [Declara√ß√µes e express√µes](#declara√ß√µes-e-express√µes)
    * [If](#if)
    * [Operador In](#operador-in)    
    * [For loop](#for-loop)
    * [Match](#match)
    * [Defer](#defer)
* [Structs](#structs)
    * [Embedded structs](#embedded-structs)
    * [Valores de campo padr√£o](#valores-de-campo-padr√£o)    
    * [Short struct literal syntax](#short-struct-initialization-syntax)
    * [Modificadores de Acesso](#modificadores-de-acesso)    
    * [M√©todos](#m√©todos)    
* [Unions](#unions)

</td><td width=33% valign=top>

* [Fun√ß√µes 2](#fun√ß√µes-2)
    * [Fun√ß√µes puras por padr√£o](#fun√ß√µes-puras-por-padr√£o)    
    * [Argumentos mut√°veis](#argumentos-mut√°veis)
    * [N√∫mero vari√°vel de argumentos](#n√∫mero-vari√°vel-de-argumentos)
    * [Fun√ß√µes an√¥nimas e de alta ordem](#fun√ß√µes-an√¥nimas-e-de-alta-ordem)
* [Refer√™ncias](#refer√™ncias)
* [Constantes](#constantes)
* [Fun√ß√µes Builtin](#fun√ß√µes-builtin)
* [Impress√£o de tipos personalizados](#impress√£o-de-tipos-personalizados)
* [M√≥dulos](#m√≥dulos)
    * [M√≥dulo Gerenciador de pacotes](#m√≥dulo-gerenciador-de-pacotes)
* [Tipos 2](#tipos-2)
    * [Interfaces](#interfaces)
    * [Enums](#enums)
    * [Sum types](#sum-types)
    * [Type aliases](#type-aliases)
    * [Option/Result types & error handling](#optionresult-types-and-error-handling)
* [Generics](#generics)
* [Concorr√™ncia](#concorr√™ncia)
    * [Spawning Concurrent Tasks](#spawning-concurrent-tasks)
    * [Channels](#channels)
    * [Objetos Compartilhados](#objetos-compartilhados)
* [Decoding JSON](#decoding-json)
* [Testing](#testing)
* [Gerenciamento de Mem√≥ria](#gerenciamento-de-mem√≥ria)
* [ORM](#orm)

</td><td valign=top>

* [Escrevendo a Documenta√ß√£o](#escrevendo-a-documenta√ß√£o)
* [Ferramentas](#ferramentas)
    * [v fmt](#v-fmt)
    * [Profiling](#profiling)
* [T√≥picos Avan√ßados](#t√≥picos-avan√ßados)
    * [Dumping expressions at runtime](#dumping-expressions-at-runtime)
    * [Memory-unsafe code](#memory-unsafe-code)
    * [Structs com campos de refer√™ncia](#structs-com-campos-de-refer√™ncia)
    * [sizeof e __offsetof](#sizeof-and-__offsetof)
    * [Chamando C do V](#chamando-c-do-v)
    * [Debugando c√≥digo C gerado](#debugando-c√≥digo-c-gerado)
    * [Compila√ß√£o Condicional](#compila√ß√£o-condicional)
    * [Compile time pseudo variables](#compile-time-pseudo-variables)
    * [Compile-time reflection](#compile-time-reflection)
    * [Limited operator overloading](#limited-operator-overloading)
    * [Inline assembly](#inline-assembly)
    * [Traduzindo C para V](#traduzindo-c-para-v)
    * [Hot code reloading](#hot-code-reloading)
    * [Cross compilation](#cross-compilation)
    * [Cross-platform shell scripts em V](#cross-platform-shell-scripts-em-v)
    * [Attributes](#attributes)
    * [Goto](#goto)
* [Ap√™ndices](#ap√™ndices)
    * [Keywords](#appendix-i-keywords)
    * [Operators](#appendix-ii-operators)

</td></tr>
</table>

<!--
Obs: existem v√°rias palavras-chave especiais,
que voc√™ pode colocar ap√≥s as cercas de c√≥digo para v:
compile, live, ignore, failcompile, oksyntax, badsyntax, wip, nofmt
Para mais detalhes, fa√ßa: `v check-md`
-->

## Hello World


```v
fn main() {
	println('hello world')
}
```

Salve este trecho em um arquivo chamado `hello.v`. Agora fa√ßa: `v execute hello.v`.

> Isso pressup√µe que voc√™ vinculou simbolicamente seu V com `v symlink`, conforme descrito
[aqui](https://github.com/vlang/v/blob/master/README.md#symlinking).
Se ainda n√£o o fez, voc√™ deve digitar o caminho para V manualmente.

Parab√©ns - voc√™ acabou de escrever e executar seu primeiro programa em V!

Voc√™ pode compilar um programa sem execu√ß√£o com `v hello.v`.
Veja `v help` para todos os comandos suportados.

No exemplo acima, voc√™ pode ver que as fun√ß√µes s√£o declaradas com a palavra-chave `fn`.
O tipo de retorno √© especificado ap√≥s o nome da fun√ß√£o.
Neste caso, `main` n√£o retorna nada, ent√£o n√£o h√° tipo de retorno.

Como em muitas outras linguagens (como C, Go e Rust), `main`
√© o ponto de entrada do seu programa.

`println` √© uma das poucas fun√ß√µes embutidas.
Ele imprime o valor passado para a sa√≠da padr√£o.

A declara√ß√£o `fn main ()` pode ser ignorada em um arquivo de programas.
Isso √© √∫til ao escrever pequenos programas, "scripts" ou apenas aprender a linguagem.
Para resumir, `fn main ()` ser√° ignorado neste tutorial.

Isso significa que um programa "hello world" em V √© t√£o simples quanto

```v
println('hello world')
```

## Executando uma pasta de projeto com v√°rios arquivos

Suponha que voc√™ tenha uma pasta com v√°rios arquivos .v, onde um deles
cont√©m sua fun√ß√£o `main ()`, e os outros arquivos t√™m outras
fun√ß√µes auxiliares. Eles podem ser organizados por t√≥picos, mas ainda * n√£o * estruturados
o suficiente para serem seus pr√≥prios m√≥dulos reutiliz√°veis ‚Äã‚Äãseparados,
e voc√™ deseja compilar
todos eles em um programa.

Em outras palavras, voc√™ teria que usar includes ou um sistema de compila√ß√£o
para enumerar todos os arquivos, compil√°-los separadamente para arquivos-objeto,
em seguida, vincul√°-los a um execut√°vel final.

No V, no entanto, voc√™ pode compilar e executar toda a pasta de arquivos .v juntos,
usando apenas `v run .`. Passar par√¢metros tamb√©m funciona, ent√£o voc√™ pode
fazer: `v run. --seuparamalgumas_outras_coisas`

O comando acima ir√° primeiro compilar seus arquivos em um √∫nico programa (chamado
ap√≥s sua pasta / projeto), e ent√£o executar√° o programa com
`--seuparamalgumas_outras_coisas` passado para ele como par√¢metros CLI.

Seu programa pode ent√£o usar os par√¢metros CLI como este:
```v
import os

println(os.args)
```
Obs: ap√≥s uma execu√ß√£o bem-sucedida, V ir√° deletar o execut√°vel gerado.
Se voc√™ quiser mant√™-lo, use `v -keepc run .` em vez disso, ou apenas compile
manualmente com `v .`.

Obs: qualquer sinalizador do compilador V deve ser passado * antes * do comando `run`.
Tudo ap√≥s o arquivo / pasta de origem, ser√° passado para o programa
como est√° - n√£o ser√° processado por V.

## Coment√°rios

```v
// Esta √© uma √∫nica linha de coment√°rio
/*
Este √© um coment√°rio multiline
   /* Pode ser aninhado. */
*/
```

## Fun√ß√µes

```v
fn main() {
	println(add(77, 33))
	println(sub(100, 50))
}

fn add(x int, y int) int {
	return x + y
}

fn sub(x int, y int) int {
	return x - y
}
```

Novamente, o tipo vem depois do nome do argumento.

Assim como em Go e C, as fun√ß√µes n√£o podem ser sobrecarregadas.
Isso simplifica o c√≥digo e melhora a capacidade de manuten√ß√£o e leitura.

As fun√ß√µes podem ser usadas antes de sua declara√ß√£o:
`add` e` sub` s√£o declarados ap√≥s `main`, mas ainda podem ser chamados de` main`.
Isso √© verdade para todas as declara√ß√µes em V e elimina a necessidade de arquivos de cabe√ßalho
ou pensando na ordem dos arquivos e declara√ß√µes.

### Retornando v√°rios valores

```v
fn foo() (int, int) {
	return 2, 3
}

a, b := foo()
println(a) // 2
println(b) // 3
c, _ := foo() // ignore os valores usando `_`
```

## Visibilidade de S√≠mbolo

```v
pub fn public_function() {
}

fn private_function() {
}
```

As fun√ß√µes s√£o privadas (n√£o exportadas) por padr√£o.
Para permitir que outros m√≥dulos as usem, acrescente `pub`. O mesmo se aplica
para constantes e tipos.

Nota: `pub` s√≥ pode ser usado a partir de um m√≥dulo nomeado.
Para obter informa√ß√µes sobre como criar um m√≥dulo, consulte [M√≥dulos] (#m√≥dulos).

## Vari√°veis

```v
name := 'Bob'
age := 20
large_number := i64(9999999999)
println(name)
println(age)
println(large_number)
```

As vari√°veis ‚Äã‚Äãs√£o declaradas e inicializadas com `:=`. Este √© o √∫nico
maneira de declarar vari√°veis ‚Äã‚Äãem V. Isso significa que as vari√°veis ‚Äã‚Äãsempre t√™m um
valor.

O tipo da vari√°vel √© inferido do valor do lado direito.
Para escolher um tipo diferente, use a convers√£o de tipo:
a express√£o `T (v)` converte o valor `v` para o tipo `T`.

Ao contr√°rio da maioria das outras linguagens, V s√≥ permite definir vari√°veis ‚Äã‚Äãem fun√ß√µes.
Vari√°veis ‚Äã‚Äãglobais (n√≠vel de m√≥dulo) n√£o s√£o permitidas. N√£o h√° estado global em V
(consulte [Fun√ß√µes puras por padr√£o](#fun√ß√µes-puras-por-padr√£o) para obter detalhes).

Para consist√™ncia em diferentes bases de c√≥digo, todos os nomes de vari√°veis ‚Äã‚Äãe fun√ß√µes
devem usar o estilo `snake_case`, ao contr√°rio dos nomes de tipo, que devem usar` PascalCase`.

### Vari√°veis Mut√°veis

```v
mut age := 20
println(age)
age = 21
println(age)
```

Para alterar o valor da vari√°vel, use `=`. Em V, as vari√°veis ‚Äã‚Äãs√£o
imut√°veis por padr√£o.
Para poder alterar o valor da vari√°vel, voc√™ deve declar√°-la com `mut`.

Tente compilar o programa acima ap√≥s remover `mut` da primeira linha.

### Inicializa√ß√£o vs atribui√ß√£o

Observe a (importante) diferen√ßa entre `: =` e `=`.
`: =` √© usado para declarar e inicializar, `=` √© usado para atribuir.

```v failcompile
fn main() {
	age = 21
}
```

Este c√≥digo n√£o ir√° compilar, porque a vari√°vel `age` n√£o est√° declarada.
Todas as vari√°veis ‚Äã‚Äãprecisam ser declaradas em V.

```v
fn main() {
	age := 21
}
```

Os valores de v√°rias vari√°veis ‚Äã‚Äãpodem ser alterados em uma linha.
Desta forma, seus valores podem ser trocados sem uma vari√°vel intermedi√°ria.

```v
mut a := 0
mut b := 1
println('$a, $b') // 0, 1
a, b = b, a
println('$a, $b') // 1, 0
```

### Erros de declara√ß√£o

No modo de desenvolvimento, o compilador ir√° avis√°-lo de que voc√™ n√£o usou a vari√°vel
(voc√™ receber√° um aviso de "vari√°vel n√£o utilizada").
No modo de produ√ß√£o (habilitado passando a sinaliza√ß√£o `-prod` para v -` v -prod foo.v`)
ele n√£o ir√° compilar (como em Go).

```v failcompile nofmt
fn main() {
	a := 10
	if true {
		a := 20 // error: redefinition of `a`
	}
	// warning: unused variable `a`
}
```

Ao contr√°rio da maioria das linguagens, o sombreamento vari√°vel n√£o √© permitido.
Declarando uma vari√°vel com um nome
que j√° √© usado em um escopo pai causar√° um erro de compila√ß√£o.

No entanto, voc√™ pode criar sombra nos m√≥dulos importados,
pois isso √© muito √∫til em algumas situa√ß√µes:
```v ignore
import ui
import gg

fn draw(ctx &gg.Context) {
	gg := ctx.parent.get_ui().gg
	gg.draw_rect(10, 10, 100, 50)
}
```

## Tipos

### Tipos Primitivos

```v ignore
bool

string

i8    i16  int  i64      i128 (soon)
byte  u16  u32  u64      u128 (soon)

rune // representa um ponto de c√≥digo Unicode

f32 f64

voidptr, size_t // estes s√£o usados ‚Äã‚Äãprincipalmente para interoperabilidade C

any // semelhante ao void * de C e √† interface de Go {}
```

Observe que, ao contr√°rio de C e Go, `int` √© sempre um n√∫mero inteiro de 32 bits.

H√° uma exce√ß√£o √† regra de que todos os operadores
em V deve ter valores do mesmo tipo em ambos os lados. Um pequeno tipo primitivo
de um lado pode ser promovido automaticamente se se encaixar
completamente no intervalo de dados do tipo do outro lado.
Estas s√£o as possibilidades permitidas:

```v ignore
   i8 ‚Üí i16 ‚Üí int ‚Üí i64
                  ‚Üò     ‚Üò
                    f32 ‚Üí f64
                  ‚Üó     ‚Üó
 byte ‚Üí u16 ‚Üí u32 ‚Üí u64 ‚¨é
      ‚Üò     ‚Üò     ‚Üò      ptr
   i8 ‚Üí i16 ‚Üí int ‚Üí i64 ‚¨è
```
Um valor `int`, por exemplo, pode ser promovido automaticamente para` f64`
ou `i64`, mas n√£o para` u32`. (`u32` significaria perda do sinal para
valores negativos).
A promo√ß√£o de `int` para` f32`, no entanto, atualmente √© feita automaticamente
(mas pode levar √† perda de precis√£o para valores grandes).

Literais como `123` ou` 4.56` s√£o tratados de uma maneira especial. Eles
n√£o levam a promo√ß√µes de tipo, no entanto, o padr√£o √© `int` e` f64`
respectivamente, quando seu tipo deve ser decidido:

```v nofmt
u := u16(12)
v := 13 + u    // v is of type `u16` - no promotion
x := f32(45.6)
y := x + 3.14  // x is of type `f32` - no promotion
a := 75        // a is of type `int` - default for int literal
b := 14.7      // b is of type `f64` - default for float literal
c := u + a     // c is of type `int` - automatic promotion of `u`'s value
d := b + x     // d is of type `f64` - automatic promotion of `x`'s value
```

### Strings

```v
name := 'Bob'
println(name.len)
println(name[0]) // indexing gives a byte B
println(name[1..3]) // slicing gives a string 'ob'
windows_newline := '\r\n' // escape special characters like in C
assert windows_newline.len == 2
```

Em V, uma string √© uma matriz de bytes somente leitura.
Os dados da string s√£o codificados usando UTF-8.
Os valores da string s√£o imut√°veis. Voc√™ n√£o pode modificar elementos:

```v failcompile
mut s := 'hello üåé'
s[0] = `H` // not allowed
```
> error: cannot assign to `s[i]` since V strings are immutable

Observe que indexar uma string produzir√° um `byte`, n√£o um` rune`.
√çndices correspondem para bytes na string, n√£o pontos de c√≥digo Unicode.

Literais de caracteres s√£o do tipo `rune`. Para denot√°-los, use `

```v
rocket := `üöÄ`
assert 'aloha!'[0] == `a`
```

As aspas simples e duplas podem ser usadas para denotar strings.
Para consist√™ncia, `vfmt` converte aspas duplas em aspas simples,
a menos que a string contenha um caractere de aspas simples.

Para strings brutas, acrescente `r`. Strings brutos n√£o s√£o escapados:

```v
s := r'hello\nworld'
println(s) // "hello\nworld"
```

Strings podem ser facilmente convertidos em inteiros:

```v
s := '42'
n := s.int() // 42
```

### Interpola√ß√£o de String

A sintaxe de interpola√ß√£o b√°sica √© muito simples - use `$`
antes do nome de uma vari√°vel.
A vari√°vel ser√° convertida em uma string e incorporada ao literal:
```v
name := 'Bob'
println('Hello, $name!') // Hello, Bob!
```
Tamb√©m funciona com os campos: `'age = $ user.age'`.
Se voc√™ precisar de express√µes mais complexas, use `$ {}`: `'can register = $ {user.age> 13}'`.

Especificadores de formato semelhantes √†queles em `printf ()` do C tamb√©m s√£o suportados.
`f`,` g`, `x`, etc. s√£o opcionais e especificam o formato de sa√≠da.
O compilador cuida do tamanho do armazenamento, para que n√£o haja `hd` ou` llu`.

```v
x := 123.4567
println('x = ${x:4.2f}')
println('[${x:10}]') // pad with spaces on the left => [   123.457]
println('[${int(x):-10}]') // pad with spaces on the right => [123       ]
println('[${int(x):010}]') // pad with zeros on the left => [0000000123]
```

### String operators

```v
name := 'Bob'
bobby := name + 'by' // + is used to concatenate strings
println(bobby) // "Bobby"
mut s := 'hello '
s += 'world' // `+=` is used to append to a string
println(s) // "hello world"
```

Todos os operadores em V devem ter valores do mesmo tipo em ambos os lados.
Voc√™ n√£o pode concatenar um inteiro com uma string:

```v failcompile
age := 10
println('age = ' + age) // not allowed
```
> error: infix expr: cannot use `int` (right expression) as `string`

Temos que converter `idade` em uma` string`:

```v
age := 11
println('age = ' + age.str())
```

ou use interpola√ß√£o de string (preferencial):

```v
age := 12
println('age = $age')
```

### N√∫meros

```v
a := 123
```

Isso atribuir√° o valor de 123 a `a`. Por padr√£o, `a` ter√° o
digite `int`.

Voc√™ tamb√©m pode usar nota√ß√£o hexadecimal, bin√°ria ou octal para literais inteiros:

```v
a := 0x7B
b := 0b01111011
c := 0o173
```

Todos eles ser√£o atribu√≠dos ao mesmo valor, 123. Todos eles ter√£o tipo
`int`, n√£o importa a nota√ß√£o que voc√™ usou.

V tamb√©m suporta a escrita de n√∫meros com `_` como separador:

```v
num := 1_000_000 // same as 1000000
three := 0b0_11 // same as 0b11
float_num := 3_122.55 // same as 3122.55
hexa := 0xF_F // same as 255
oct := 0o17_3 // same as 0o173
```

Se quiser um tipo diferente de n√∫mero inteiro, voc√™ pode usar a convers√£o:

```v
a := i64(123)
b := byte(42)
c := i16(12345)
```

A atribui√ß√£o de n√∫meros de ponto flutuante funciona da mesma maneira:

```v
f := 1.0
f1 := f64(3.14)
f2 := f32(3.14)
```
Se voc√™ n√£o especificar o tipo explicitamente, por padr√£o, literais flutuantes
ter√° o tipo `f64`.

### Arrays

```v
mut nums := [1, 2, 3]
println(nums) // "[1, 2, 3]"
println(nums[1]) // "2"
nums[1] = 5
println(nums) // "[1, 5, 3]"
println(nums.len) // "3"
nums = [] // The array is now empty
println(nums.len) // "0"
// Declare an empty array:
users := []int{}
```

O tipo de uma matriz √© determinado pelo primeiro elemento:
* `[1, 2, 3]` √© uma matriz de ints (`[] int`).
* `['a', 'b']` √© um array de strings (`[] string`).

O usu√°rio pode especificar explicitamente o tipo para o primeiro elemento:
`[byte (16), 32, 64, 128]`.
As matrizes V s√£o homog√™neas (todos os elementos devem ter o mesmo tipo).
Isso significa que c√≥digo como `[1, 'a']` n√£o ser√° compilado.

O campo `.len` retorna o comprimento da matriz.
Observe que √© um campo somente leitura,
e n√£o pode ser modificado pelo usu√°rio.
Os campos exportados s√£o somente leitura por padr√£o em V.
Veja [modificadores de acesso] (#modificadores-de-acesso).

#### Array operations

```v
mut nums := [1, 2, 3]
nums << 4
println(nums) // "[1, 2, 3, 4]"
// append array
nums << [5, 6, 7]
println(nums) // "[1, 2, 3, 4, 5, 6, 7]"
mut names := ['John']
names << 'Peter'
names << 'Sam'
// names << 10  <-- This will not compile. `names` is an array of strings.
println(names.len) // "3"
println('Alex' in names) // "false"
```

`<<`  √© um operador que anexa um valor ao final da matriz.
Ele tamb√©m pode anexar uma matriz inteira.

`val in array` retorna verdadeiro se o array cont√©m` val`.
Veja [operador `in`](#operador-in).

#### Inicializando propriedades de array

Durante a inicializa√ß√£o, voc√™ pode especificar a capacidade da matriz (`cap`),
seu comprimento inicial (` len`),
e o elemento padr√£o (`init`):

```v
arr := []int{len: 5, init: -1}
// `[-1, -1, -1, -1, -1]`
```

Definir a capacidade melhora o desempenho das inser√ß√µes,
pois reduz o n√∫mero de realoca√ß√µes necess√°rias:

```v
mut numbers := []int{cap: 1000}
println(numbers.len) // 0
// Now appending elements won't reallocate
for i in 0 .. 1000 {
	numbers << i
}
```
Nota: O c√≥digo acima usa uma instru√ß√£o [range `for`](#range-for).

#### M√©todos do Array

Todos os arrays podem ser facilmente impressos com `println (arr)`
e convertidos em uma string
com `s: = arr.str ()`.

Copiar os dados do array √© feito com `.clone ()`:

```v
nums := [1, 2, 3]
nums_copy := nums.clone()
```

Arrays podem ser filtrados e mapeados de forma eficiente com `.filter ()` e
m√©todos `.map ()`:

```v
nums := [1, 2, 3, 4, 5, 6]
even := nums.filter(it % 2 == 0)
println(even) // [2, 4, 6]
// filter pode aceitar fun√ß√µes an√¥nimas
even_fn := nums.filter(fn (x int) bool {
	return x % 2 == 0
})
println(even_fn)
words := ['hello', 'world']
upper := words.map(it.to_upper())
println(upper) // ['HELLO', 'WORLD']
// map tamb√©m pode aceitar fun√ß√µes an√¥nimas
upper_fn := words.map(fn (w string) string {
	return w.to_upper()
})
println(upper_fn) // ['HELLO', 'WORLD']
```

`it` √© uma vari√°vel embutida que se refere ao elemento
atualmente sendo processado nos m√©todos de filtro/mapa.

Al√©m disso, `.any ()` e `.all ()` podem ser usados ‚Äã‚Äãpara testar convenientemente
para elementos que satisfa√ßam uma condi√ß√£o.

```v
nums := [1, 2, 3]
println(nums.any(it == 2)) // true
println(nums.all(it >= 2)) // false
```

#### Arrays Multidimensionais

Arrays podem ter mais de uma dimens√£o.

Exemplo 2d array:
```v
mut a := [][]int{len: 2, init: []int{len: 3}}
a[0][1] = 2
println(a) // [[0, 2, 0], [0, 0, 0]]
```

Exemplo 3d array:
```v
mut a := [][][]int{len: 2, init: [][]int{len: 3, init: []int{len: 2}}}
a[0][1][1] = 2
println(a) // [[[0, 0], [0, 2], [0, 0]], [[0, 0], [0, 0], [0, 0]]]
```

#### Sorting arrays

Sorting arrays de todos os tipos s√£o muito simples e intuitivos.
Vari√°veis ‚Äã‚Äãespeciais `a` e` b`
s√£o usados ‚Äã‚Äãao fornecer uma condi√ß√£o de classifica√ß√£o personalizada.

```v
mut numbers := [1, 3, 2]
numbers.sort() // 1, 2, 3
numbers.sort(a > b) // 3, 2, 1
```

```v
struct User {
	age  int
	name string
}

mut users := [User{21, 'Bob'}, User{20, 'Zarkon'}, User{25, 'Alice'}]
users.sort(a.age < b.age) // sort by User.age int field
users.sort(a.name > b.name) // reverse sort by User.name string field
```

#### Array Slices

Slices s√£o arrays parciais. Eles representam cada elemento entre dois √≠ndices
separados por um operador ... O √≠ndice do lado direito deve ser maior ou igual
para o √≠ndice do lado esquerdo.

Se um √≠ndice do lado direito estiver ausente, ser√° considerado o comprimento da matriz.
Se um √≠ndice do lado esquerdo est√° ausente, √© assumido como 0.

```v
nums := [0, 10, 20, 30, 40]
println(nums[1..4]) // [10, 20, 30]
println(nums[..4]) // [0, 10, 20, 30]
println(nums[1..]) // [10, 20, 30, 40]
```

Todas as opera√ß√µes de array podem ser realizadas em fatias.
As fatias podem ser colocadas em uma matriz do mesmo tipo.

```v
array_1 := [3, 5, 4, 7, 6]
mut array_2 := [0, 1]
array_2 << array_1[..3]
println(array_2) // [0, 1, 3, 5, 4]
```

### Arrays de tamanho fixo

V tamb√©m oferece suporte a arrays com tamanho fixo. Ao contr√°rio de arrays comuns,
seus o comprimento √© constante. Voc√™ n√£o pode anexar elementos a eles, nem reduzi-los.
Voc√™ s√≥ pode modificar seus elementos no local.

No entanto, o acesso aos elementos de arrays de tamanho fixo √© mais eficiente,
eles precisam de menos mem√≥ria do que arrays comuns e, ao contr√°rio de matrizes comuns,
seus dados est√£o na pilha, ent√£o voc√™ pode querer us√°-los como buffers se voc√™
n√£o deseja aloca√ß√µes de heap adicionais.

A maioria dos m√©todos √© definida para funcionar em arrays comuns, n√£o em arrays de tamanho fixo.
Voc√™ pode converter um array de tamanho fixo em um array comum com fatiamento:
```v
mut fnums := [3]int{} // fnums √© um array de tamanho fixo com 03 elementos.
fnums[0] = 1
fnums[1] = 10
fnums[2] = 100
println(fnums) // => [1, 10, 100]
println(typeof(fnums).name) // => [3]int

fnums2 := [1, 10, 100]! // short init syntax that does the same (the syntax will probably change)

anums := fnums[0..fnums.len]
println(anums) // => [1, 10, 100]
println(typeof(anums).name) // => []int
```
Observe que o fatiamento far√° com que os dados da matriz de tamanho fixo sejam copiados para
o array comum rec√©m-criado.

### Maps

```v
mut m := map[string]int{} // um map com chaves `string` e valores `int`
m['one'] = 1
m['two'] = 2
println(m['one']) // "1"
println(m['bad_key']) // "0"
println('bad_key' in m) // Use `in` para detectar se essa chave existe
m.delete('two')
```
Os maps podem ter chaves do tipo string, rune, integer, float ou voidptr.

Todo o mapa pode ser inicializado usando esta sintaxe curta:
```v
numbers := map{
	'one': 1
	'two': 2
}
println(numbers)
```

Se uma chave n√£o for encontrada, um valor zero √© retornado por padr√£o:

```v
sm := map{
	'abc': 'xyz'
}
val := sm['bad_key']
println(val) // ''
```
```v
intm := map{
	1: 1234
	2: 5678
}
s := intm[3]
println(s) // 0
```

Tamb√©m √© poss√≠vel usar um bloco `or{}` para lidar com as chaves ausentes:

```v
mm := map[string]int{}
val := mm['bad_key'] or { panic('key not found') }
```

A mesma verifica√ß√£o opcional se aplica a arrays:

```v
arr := [1, 2, 3]
large_index := 999
val := arr[large_index] or { panic('out of bounds') }
```

## Importa√ß√£o de M√≥dulos

Para obter informa√ß√µes sobre como criar um m√≥dulo, consulte [M√≥dulos](#m√≥dulos).

Os m√≥dulos podem ser importados usando a palavra-chave `import`:

```v
import os

fn main() {
	// l√™ o texto a partir do stdin
	name := os.input('Enter your name: ')
	println('Hello, $name!')
}
```
Este programa pode usar qualquer defini√ß√£o p√∫blica do m√≥dulo `os`, como
a fun√ß√£o `input`. Veja a documenta√ß√£o da [biblioteca padr√£o](https://modules.vlang.io/)
para uma lista de m√≥dulos comuns e seus s√≠mbolos p√∫blicos.

Por padr√£o, voc√™ deve especificar o prefixo do m√≥dulo sempre que chamar uma fun√ß√£o externa.
Isso pode parecer prolixo no in√≠cio, mas torna o c√≥digo muito mais leg√≠vel
e mais f√°cil de entender - √© sempre claro qual fun√ß√£o de
qual m√≥dulo est√° sendo chamado. Isso √© especialmente √∫til em grandes bases de c√≥digo.

Importa√ß√µes de m√≥dulos c√≠clicos n√£o s√£o permitidas, como em Go.

### Importa√ß√µes seletivas

Voc√™ tamb√©m pode importar fun√ß√µes e tipos espec√≠ficos de m√≥dulos diretamente:

```v
import os { input }

fn main() {
	// l√™ o texto a partir do stdin
	name := input('Enter your name: ')
	println('Hello, $name!')
}
```
Nota: Isso n√£o √© permitido para constantes - elas devem ser sempre prefixadas.

Voc√™ pode importar v√°rios s√≠mbolos espec√≠ficos de uma vez:

```v
import os { input, user_os }

name := input('Enter your name: ')
println('Name: $name')
os := user_os()
println('Your OS is ${os}.')
```

### Aliasing de importa√ß√£o de m√≥dulo

Qualquer nome de m√≥dulo importado pode receber um alias usando a palavra-chave `as`:

NOTA: este exemplo n√£o ir√° compilar a menos que voc√™ tenha criado `mymod/sha256.v`
```v failcompile
import crypto.sha256
import mymod.sha256 as mysha256

fn main() {
	v_hash := mysha256.sum('hi'.bytes()).hex()
	my_hash := mysha256.sum('hi'.bytes()).hex()
	assert my_hash == v_hash
}
```

Voc√™ n√£o pode criar um alias para uma fun√ß√£o ou tipo importado.
No entanto, voc√™ _pode_ redeclarar um tipo.

```v
import time
import math

type MyTime = time.Time

fn (mut t MyTime) century() int {
	return int(1.0 + math.trunc(f64(t.year) * 0.009999794661191))
}

fn main() {
	mut my_time := MyTime{
		year: 2020
		month: 12
		day: 25
	}
	println(time.new_time(my_time).utc_string())
	println('Century: $my_time.century()')
}
```

## Declara√ß√µes e express√µes

### If

```v
a := 10
b := 20
if a < b {
	println('$a < $b')
} else if a > b {
	println('$a > $b')
} else {
	println('$a == $b')
}
```

As instru√ß√µes `if` s√£o bastante diretas e semelhantes √† maioria das outras linguagens.
Ao contr√°rio de outras linguagens semelhantes a C,
n√£o h√° par√™nteses ao redor da condi√ß√£o e as chaves s√£o sempre necess√°rias.

`if` pode ser usado como uma express√£o:

```v
num := 777
s := if num % 2 == 0 { 'even' } else { 'odd' }
println(s)
// "odd"
```

#### Verifica√ß√µes de tipo e convers√£o
Voc√™ pode verificar o tipo atual de um tipo de soma usando `is` e sua forma negada`!Is`.

Voc√™ pode fazer isso em um `if`:
```v
struct Abc {
	val string
}

struct Xyz {
	foo string
}

type Alphabet = Abc | Xyz

x := Alphabet(Abc{'test'}) // sum type
if x is Abc {
	// x √© automaticamente "casteado" para Abc e pode ser usado aqui
	println(x)
}
if x !is Abc {
	println('Not Abc')
}
```
ou usando `match`:
```v oksyntax
match x {
	Abc {
		// x √© automaticamente "casteado" para Abc e pode ser usado aqui
		println(x)
	}
	Xyz {
		// x √© automaticamente "casteado" para Xyz e pode ser usado aqui
		println(x)
	}
}
```

Isso tamb√©m funciona com campos de struct
```v
struct MyStruct {
	x int
}

struct MyStruct2 {
	y string
}

type MySumType = MyStruct | MyStruct2

struct Abc {
	bar MySumType
}

x := Abc{
	bar: MyStruct{123} // MyStruct ser√° convertido para MySumType automaticamente
}
if x.bar is MyStruct {
	// x.bar √© automaticamente "casteado"
	println(x.bar)
}
match x.bar {
	MyStruct {
		// x.bar √© automaticamente "casteado"
		println(x.bar)
	}
	else {}
}
```

Vari√°veis ‚Äã‚Äãmut√°veis ‚Äã‚Äãpodem mudar. E fazer uma convers√£o n√£o seria seguro.
No entanto, √†s vezes √© √∫til "castear" o tipo apesar da mutabilidade.
Nesses casos, o desenvolvedor deve marcar a express√£o com a palavra-chave `mut`
para dizer ao compilador que eles sabem o que est√£o fazendo.

Funciona assim:
```v oksyntax
mut x := MySumType(MyStruct{123})
if mut x is MyStruct {
	// x is casted to MyStruct even if it's mutable
	// without the mut keyword that wouldn't work
	println(x)
}
// same with match
match mut x {
	MyStruct {
		// x is casted to MyStruct even it's mutable
		// without the mut keyword that wouldn't work
		println(x)
	}
}
```

### Operador In

`in` permite verificar se um array ou map cont√©m um elemento.
Para fazer o oposto, use `!In`.

```v
nums := [1, 2, 3]
println(1 in nums) // true
println(4 !in nums) // true
m := map{
	'one': 1
	'two': 2
}
println('one' in m) // true
println('three' !in m) // true
```

Tamb√©m √© √∫til para escrever express√µes booleanas mais claras e compactas:

```v
enum Token {
	plus
	minus
	div
	mult
}

struct Parser {
	token Token
}

parser := Parser{}
if parser.token == .plus || parser.token == .minus || parser.token == .div || parser.token == .mult {
	// ...
}
if parser.token in [.plus, .minus, .div, .mult] {
	// ...
}
```

V otimiza tais express√µes,
ent√£o ambas as instru√ß√µes `if` acima
produzem o mesmo c√≥digo de m√°quina e nenhum array √© criado.

### For loop

V tem apenas uma palavra-chave de loop: `for`, com v√°rias formas.

#### `for`/`in`

Esta √© a forma mais comum. Voc√™ pode us√°-lo com um array, map ou
intervalo num√©rico.

##### Array `for`

```v
numbers := [1, 2, 3, 4, 5]
for num in numbers {
	println(num)
}
names := ['Sam', 'Peter']
for i, name in names {
	println('$i) $name')
	// Output: 0) Sam
	//         1) Peter
}
```

A forma `for value in arr` √© usada para percorrer os elementos de um array.
Se um √≠ndice for necess√°rio, uma forma alternativa `for index, value in arr` pode ser usada.

Observe que o valor √© somente leitura.
Se precisar modificar o array durante o loop, voc√™ precisar√° declarar o elemento como mut√°vel:

```v
mut numbers := [0, 1, 2]
for mut num in numbers {
	num++
}
println(numbers) // [1, 2, 3]
```
Quando um identificador √© apenas um √∫nico underscore, ele √© ignorado.

##### Map `for`

```v
m := map{
	'one': 1
	'two': 2
}
for key, value in m {
	println('$key -> $value')
	// Output: one -> 1
	//         two -> 2
}
```

A chave ou o valor podem ser ignorados usando um √∫nico underscore como identificador.
```v
m := map{
	'one': 1
	'two': 2
}
// itera sobre as chaves
for key, _ in m {
	println(key)
	// Output: one
	//         two
}
// itera sobre os valores
for _, value in m {
	println(value)
	// Output: 1
	//         2
}
```

##### Range `for`

```v
// Prints '01234'
for i in 0 .. 5 {
	print(i)
}
```
`low..high` significa um intervalo * exclusivo *, que representa todos os valores
de `low` at√© * mas n√£o incluindo *`high`.

#### Condition `for`

```v
mut sum := 0
mut i := 0
for i <= 100 {
	sum += i
	i++
}
println(sum) // "5050"
```

Esta forma de loop √© semelhante aos loops `while` em outras linguagens.
O loop ir√° parar de iterar assim que a condi√ß√£o booleana for avaliada como falsa.
Novamente, n√£o h√° par√™nteses em torno da condi√ß√£o e as chaves s√£o sempre necess√°rias.

#### Bare `for`

```v
mut num := 0
for {
	num += 2
	if num >= 10 {
		break
	}
}
println(num) // "10"
```

A condi√ß√£o pode ser omitida, resultando em um loop infinito.

#### C `for`

```v
for i := 0; i < 10; i += 2 {
	// Don't print 6
	if i == 6 {
		continue
	}
	println(i)
}
```

Por fim, existe o tradicional loop `for` do estilo C. √â mais seguro do que a forma `while`
porque com o √∫ltimo √© f√°cil esquecer de atualizar o contador e ficar
preso em um loop infinito.

Aqui, `i` n√£o precisa ser declarado com `mut`, pois sempre ser√° mut√°vel por defini√ß√£o.

#### Labelled break & continue

`break` e` continue` controlam o loop `for` mais interno por padr√£o.
Voc√™ tamb√©m pode usar `break` e` continue`
seguido por um nome de r√≥tulo para se referir a um `for` externo
ciclo:

```v
outer: for i := 4; true; i++ {
	println(i)
	for {
		if i < 7 {
			continue outer
		} else {
			break outer
		}
	}
}
```
O r√≥tulo deve preceder imediatamente o loop externo.
O c√≥digo acima √© impresso:
```
4
5
6
7
```

### Match

```v
os := 'windows'
print('V is running on ')
match os {
	'darwin' { println('macOS.') }
	'linux' { println('Linux.') }
	else { println(os) }
}
```

Uma declara√ß√£o de correspond√™ncia √© uma maneira mais curta
de escrever uma sequ√™ncia de declara√ß√µes `if - else`.
Quando uma ramifica√ß√£o correspondente for encontrada,
o seguinte bloco de instru√ß√£o ser√° executado.
O outro branch ser√° executado quando nenhum outro branch corresponder.

```v
number := 2
s := match number {
	1 { 'one' }
	2 { 'two' }
	else { 'many' }
}
```

Uma express√£o de correspond√™ncia retorna o valor
da express√£o final da ramifica√ß√£o correspondente.

```v
enum Color {
	red
	blue
	green
}

fn is_red_or_blue(c Color) bool {
	return match c {
		.red, .blue { true } // v√≠rgula pode ser usada para testar v√°rios valores
		.green { false }
	}
}
```

Uma declara√ß√£o de correspond√™ncia tamb√©m pode ser usada
para ramificar nas variantes de um `enum`
usando a sintaxe abreviada `.variant_here`.
Um branch `else` n√£o √© permitido
quando todos os ramos s√£o exaustivos.

```v
c := `v`
typ := match c {
	`0`...`9` { 'digit' }
	`A`...`Z` { 'uppercase' }
	`a`...`z` { 'lowercase' }
	else { 'other' }
}
println(typ)
// 'lowercase'
```

Voc√™ tamb√©m pode usar intervalos como padr√µes `match`. Se o valor estiver dentro do intervalo
de uma ramifica√ß√£o, essa ramifica√ß√£o ser√° executada.

Observe que os intervalos usam `...` (tr√™s pontos) em vez de `..` (dois pontos). Isso √©
porque o intervalo √© * inclusivo * do √∫ltimo elemento, em vez de exclusivo
(como os intervalos `..` s√£o). Usar `..` em um branch de correspond√™ncia gerar√° um erro.

Nota: `match` como uma express√£o n√£o √© utiliz√°vel em loop` for` e declara√ß√µes `if`.

### Defer

Uma declara√ß√£o defer adia a execu√ß√£o de um bloco de declara√ß√µes
at√© que a fun√ß√£o circundante retorne.

```v
import os

fn read_log() {
	mut ok := false
	mut f := os.open('log.txt') or { panic(err.msg) }
	defer {
		f.close()
	}
	// ...
	if !ok {
		// defer statement ser√° chamado aqui e o arquivo ser√° fechado
		return
	}
	// ...
	// defer statement ser√° chamado aqui e o arquivo ser√° fechado
}
```

Se a fun√ß√£o retornar um valor, o bloco `defer` √© executado * ap√≥s * o retorno
express√£o √© avaliada:

```v
import os

enum State {
	normal
	write_log
	return_error
}

// escreve o arquivo de log e retorna o n√∫mero de bytes escrito
fn write_log(s State) ?int {
	mut f := os.create('log.txt') ?
	defer {
		f.close()
	}
	if s == .write_log {
		// `f.close()` ser√° chamado ap√≥s `f.write()` ser executado,
		// mas antes `write_log()` finalmente retorna o n√∫mero de bytes
		// escrito para o `main()`
		return f.writeln('This is a log file')
	} else if s == .return_error {
		// o arquivo ser√° fechado ap√≥s a fun√ß√£o `error()`
		// ter retornado - portanto, a mensagem de erro ainda o relatar√° como aberto
		return error('nothing written; file open: $f.is_opened')
	}
	// o arquivo tamb√©m ser√° fechado aqui
	return 0
}

fn main() {
	n := write_log(.return_error) or {
		println('Error: $err')
		0
	}
	println('$n bytes written')
}
```

## Structs

```v
struct Point {
	x int
	y int
}

mut p := Point{
	x: 10
	y: 20
}
println(p.x) // Struct fields are accessed using a dot
// Sintaxy literal alternativa para struct com 03 campos ou menos
p = Point{10, 20}
assert p.x == 10
```

### Heap structs

Structs s√£o alocados na stack. Para alocar uma estrutura no heap
e obter uma refer√™ncia a ele, use o prefixo `&`:

```v
struct Point {
	x int
	y int
}

p := &Point{10, 10}
// Refer√™ncias tem a mesma sintaxe para acessar os campos
println(p.x)
```

O tipo de `p` √©` & Point`. √â uma [refer√™ncia](#refer√™ncias) para `Point`.
As refer√™ncias s√£o semelhantes aos ponteiros Go e refer√™ncias C++.

### Embedded structs

V n√£o permite subclasses, mas suporta estruturas incorporadas:

```v
struct Widget {
mut:
	x int
	y int
}

struct Button {
	Widget
	title string
}

mut button := Button{
	title: 'Click me'
}
button.x = 3
```
Sem incorporar, ter√≠amos que nomear o campo `Widget` e fazer:

```v oksyntax
button.widget.x = 3
```

### Default field values

```v
struct Foo {
	n   int    // n √© 0 por padr√£o
	s   string // s √© '' por padr√£o
	a   []int  // a √© `[]int{}` por padr√£o
	pos int = -1 // valor padr√£o customizado
}
```

Todos os campos de structs s√£o zerados por padr√£o durante a cria√ß√£o da struct.
Campos de array e map s√£o alocados.

Tamb√©m √© poss√≠vel definir valores padr√£o personalizados.

### Required fields

```v
struct Foo {
	n int [required]
}
```

Voc√™ pode marcar um campo de estrutura com o atributo `[required]`, para dizer ao V que
esse campo deve ser inicializado ao criar uma inst√¢ncia dessa estrutura.

Este exemplo n√£o ir√° compilar, uma vez que o campo `n` n√£o foi inicializado explicitamente:
```v failcompile
_ = Foo{}
```

<a id='short-struct-initialization-syntax' />

### Short struct literal syntax

```v
struct Point {
	x int
	y int
}

mut p := Point{
	x: 10
	y: 20
}
// voc√™ pode omitir o nome do struct quando j√° √© conhecido
p = {
	x: 30
	y: 4
}
assert p.y == 4
```

Omitir o nome da estrutura tamb√©m funciona para retornar uma estrutura literal ou passar uma
como um argumento de fun√ß√£o.

#### Trailing struct literal arguments

V n√£o tem argumentos de fun√ß√£o padr√£o ou argumentos nomeados, para essa estrutura final
a sintaxe literal pode ser usada em seu lugar:

```v
struct ButtonConfig {
	text        string
	is_disabled bool
	width       int = 70
	height      int = 20
}

struct Button {
	text   string
	width  int
	height int
}

fn new_button(c ButtonConfig) &Button {
	return &Button{
		width: c.width
		height: c.height
		text: c.text
	}
}

button := new_button(text: 'Click me', width: 100)
// o height n√£o est√° definido, ent√£o √© o valor padr√£o
assert button.height == 20
```

Como voc√™ pode ver, o nome da estrutura e as chaves podem ser omitidos, em vez de:

```v oksyntax nofmt
new_button(ButtonConfig{text:'Click me', width:100})
```

Isso s√≥ funciona para fun√ß√µes que usam uma estrutura para o √∫ltimo argumento.

### Access modifiers

Os campos Struct s√£o privados
e imut√°veis ‚Äã‚Äãpor padr√£o (tornando os structs imut√°veis ‚Äã‚Äãtamb√©m).
Seus modificadores de acesso podem ser alterados com
`pub` e` mut`. No total, existem 05 op√ß√µes poss√≠veis:

```v
struct Foo {
	a int // private immutable (default)
mut:
	b int // private mutable
	c int // (voc√™ pode listar v√°rios campos com o mesmo modificador de acesso)
pub:
	d int // public immutable (readonly)
pub mut:
	e int // public, mas mut√°vel somente no m√≥dulo pai
__global:
	// (n√£o √© recomendado usar. Por isso, a palavra-chave 'global' come√ßa com __)
	f int // public e mut√°vel ambos dentro e fora do m√≥dulo pai
}
```

Por exemplo, aqui est√° o tipo `string` definido no m√≥dulo `builtin`:

```v ignore
struct string {
    str &byte
pub:
    len int
}
```

√â f√°cil ver a partir desta defini√ß√£o que `string` √© um tipo imut√°vel.
O ponteiro de byte com os dados da string n√£o pode ser acessado
fora de `builtin` de forma alguma.
O campo `len` √© p√∫blico, mas imut√°vel:
```v failcompile
fn main() {
	str := 'hello'
	len := str.len // OK
	str.len++ // Compilation error
}
```

Isso significa que definir campos p√∫blicos somente leitura √© muito f√°cil em V,
n√£o h√° necessidade de getters / setters ou propriedades.

## M√©todos

```v
struct User {
	age int
}

fn (u User) can_register() bool {
	return u.age > 16
}

user := User{
	age: 10
}
println(user.can_register()) // "false"
user2 := User{
	age: 20
}
println(user2.can_register()) // "true"
```

V n√£o tem classes, mas voc√™ pode definir m√©todos em tipos.
Um m√©todo √© uma fun√ß√£o com um argumento receptor especial.
O receptor aparece em sua pr√≥pria lista de argumentos entre
a palavra-chave `fn` e o nome do m√©todo.
Os m√©todos devem estar no mesmo m√≥dulo que o tipo de receptor.

Neste exemplo, o m√©todo `can_register` possui um
receptor do tipo `User` chamado `u`.
A conven√ß√£o n√£o √© usar nomes de receptores como `self` ou` this`,
mas um nome curto, de prefer√™ncia uma letra.

## Unions

Assim como structs, os unions apoiam a incorpora√ß√£o.

```v
struct Rgba32_Component {
	r byte
	g byte
	b byte
	a byte
}

union Rgba32 {
	Rgba32_Component
	value u32
}

clr1 := Rgba32{
	value: 0x008811FF
}

clr2 := Rgba32{
	Rgba32_Component: {
		a: 128
	}
}

sz := sizeof(Rgba32)
unsafe {
	println('Size: ${sz}B,clr1.b: $clr1.b,clr2.b: $clr2.b')
}
```

Output: `Size: 4B, clr1.b: 136, clr2.b: 0`

O acesso de membro do union deve ser realizado em um bloco "unsafe".

Observe que os argumentos de estrutura incorporados n√£o s√£o
necessariamente armazenados na ordem listada.

## Fun√ß√µes 2

### Fun√ß√µes puras por padr√£o

As fun√ß√µes V s√£o puras por padr√£o, o que significa que seus
valores de retorno s√£o uma fun√ß√£o de seus
argumentos apenas, e sua avalia√ß√£o n√£o tem efeitos colaterais (al√©m de I/O).

Isso √© conseguido pela falta de vari√°veis ‚Äã‚Äãglobais e todos os argumentos de fun√ß√£o sendo
imut√°vel por padr√£o, mesmo quando [refer√™ncias](#refer√™ncias) s√£o passadas.

V n√£o √© uma linguagem puramente funcional.

H√° um sinalizador do compilador para habilitar vari√°veis ‚Äã‚Äãglobais (`-enable-globals`),
mas este √© destinado a aplicativos de baixo n√≠vel como kernels e drivers.

### Argumentos Mut√°veis

√â poss√≠vel modificar os argumentos da fun√ß√£o usando a palavra-chave `mut`:

```v
struct User {
	name string
mut:
	is_registered bool
}

fn (mut u User) register() {
	u.is_registered = true
}

mut user := User{}
println(user.is_registered) // "false"
user.register()
println(user.is_registered) // "true"
```

Neste exemplo, o receptor (que √© simplesmente o primeiro argumento)
√© marcado como mut√°vel,
ent√£o `register()` pode mudar o objeto do usu√°rio.
O mesmo funciona com argumentos de n√£o receptores:

```v
fn multiply_by_2(mut arr []int) {
	for i in 0 .. arr.len {
		arr[i] *= 2
	}
}

mut nums := [1, 2, 3]
multiply_by_2(mut nums)
println(nums)
// "[2, 4, 6]"
```

Observe que voc√™ deve adicionar `mut` antes de` nums` ao chamar esta fun√ß√£o. Isto faz
ficar claro que a fun√ß√£o que est√° sendo chamada modificar√° o valor.

√â prefer√≠vel retornar valores em vez de modificar argumentos.
A modifica√ß√£o de argumentos s√≥ deve ser feita em partes
cr√≠ticas de desempenho de seu aplicativo
para reduzir as aloca√ß√µes e c√≥pias.

Por esta raz√£o, V n√£o permite a modifica√ß√£o de argumentos
com tipos primitivos (por exemplo, inteiros).
Apenas tipos mais complexos, como arrays e maps, podem ser modificados.

Use `user.register()` ou `user = register(user)`
em vez de `register (user mut)`.

#### Struct update syntax

V torna mais f√°cil retornar uma vers√£o modificada de um objeto:

```v
struct User {
	name          string
	age           int
	is_registered bool
}

fn register(u User) User {
	return {
		...u
		is_registered: true
	}
}

mut user := User{
	name: 'abc'
	age: 23
}
user = register(user)
println(user)
```

### N√∫mero vari√°vel de argumentos

```v
fn sum(a ...int) int {
	mut total := 0
	for x in a {
		total += x
	}
	return total
}

println(sum()) // 0
println(sum(1)) // 1
println(sum(2, 3)) // 5
// usando decomposi√ß√£o de array
a := [2, 3, 4]
println(sum(...a)) // <-- usando prefix ... aqui. output: 9
b := [5, 6, 7]
println(sum(...b)) // output: 18
```

### Fun√ß√µes an√¥nimas e de alta ordem

```v
fn sqr(n int) int {
	return n * n
}

fn cube(n int) int {
	return n * n * n
}

fn run(value int, op fn (int) int) int {
	return op(value)
}

fn main() {
	// Fun√ß√µes podem ser passadas para outras fun√ß√µes
	println(run(5, sqr)) // "25"
	// Fun√ß√µes an√¥nimas podem ser declaradas dentro de outras fun√ß√µes:
	double_fn := fn (n int) int {
		return n + n
	}
	println(run(5, double_fn)) // "10"
	// Fun√ß√µes podem ser transmitidas sem atribu√≠-las a vari√°veis:
	res := run(5, fn (n int) int {
		return n + n
	})
	println(res) // "10"
	// Voc√™ pode at√© ter um array/map de fun√ß√µes:
	fns := [sqr, cube]
	println(fns[0](10)) // "100"
	fns_map := map{
		'sqr':  sqr
		'cube': cube
	}
	println(fns_map['cube'](2)) // "8"
}
```

## Refer√™ncias

```v
struct Foo {}

fn (foo Foo) bar_method() {
	// ...
}

fn bar_function(foo Foo) {
	// ...
}
```

Se um argumento de fun√ß√£o for imut√°vel (como `foo` nos exemplos acima)
V pode passar por valor ou por refer√™ncia. O compilador decidir√°,
e o desenvolvedor n√£o precisa pensar nisso.

Voc√™ n√£o precisa mais se lembrar se deve passar a estrutura por valor
ou por refer√™ncia.

Voc√™ pode garantir que a estrutura seja sempre passada por refer√™ncia
adicionando `&`:

```v
struct Foo {
	abc int
}

fn (foo &Foo) bar() {
	println(foo.abc)
}
```

`foo` ainda √© imut√°vel e n√£o pode ser alterado. Por isso,
`(mut foo Foo)` deve ser usado.

Em geral, as refer√™ncias de V s√£o semelhantes aos ponteiros Go e refer√™ncias C++.
Por exemplo, uma defini√ß√£o de estrutura de √°rvore gen√©rica seria assim:

```v wip
struct Node<T> {
    val   T
    left  &Node
    right &Node
}
```

## Constantes

```v
const (
	pi    = 3.14
	world = '‰∏ñÁïå'
)

println(pi)
println(world)
```

As constantes s√£o declaradas com `const`. Eles s√≥ podem ser definidos
no n√≠vel do m√≥dulo (fora das fun√ß√µes).
Os valores constantes nunca podem ser alterados. Voc√™ tamb√©m pode declarar um √∫nico
constante separadamente:

```v
const e = 2.71828
```

As constantes V s√£o mais flex√≠veis do que na maioria das linguagens.
Voc√™ pode atribuir valores mais complexos:

```v
struct Color {
	r int
	g int
	b int
}

fn rgb(r int, g int, b int) Color {
	return Color{
		r: r
		g: g
		b: b
	}
}

const (
	numbers = [1, 2, 3]
	red     = Color{
		r: 255
		g: 0
		b: 0
	}
	// evaluate function call at compile-time*
	blue = rgb(0, 0, 255)
)

println(numbers)
println(red)
println(blue)
```
\* WIP - por enquanto, as chamadas de fun√ß√£o s√£o avaliadas na inicializa√ß√£o do programa

Vari√°veis ‚Äã‚Äãglobais normalmente n√£o s√£o permitidas, ent√£o isso pode ser muito √∫til.

### Prefixo de m√≥dulo necess√°rio

Ao nomear constantes, `snake_case` deve ser usado. A fim de distinguir constantes
a partir de vari√°veis ‚Äã‚Äãlocais, o caminho completo para consts deve ser especificado.
Por exemplo, para acessar o PI const, o nome completo `math.pi` deve ser usado fora de` math`
m√≥dulo e dentro dele. Essa restri√ß√£o √© relaxada apenas para o m√≥dulo `main`
(aquele que cont√©m seu `fn main()`), onde voc√™ pode usar o nome n√£o qualificado de
constantes definidas l√°, ou seja, `numbers`, em vez de` main.numbers`.

vfmt cuida desta regra, ent√£o voc√™ pode digitar `println (pi)` dentro do m√≥dulo `math`,
e o vfmt ir√° atualiz√°-lo automaticamente para `println (math.pi)`.

<!--
Muitas pessoas preferem todas as consts em mai√∫sculas: `TOP_CITIES`. Isso n√£o funcionaria
bem em V, porque consts s√£o muito mais poderosos do que em outras linguagens.
Eles podem representar estruturas complexas, e isso √© usado com bastante frequ√™ncia, uma vez que
n√£o s√£o globais:

```v oksyntax
println('Top cities: ${top_cities.filter(.usa)}')
```
-->

## Fun√ß√µes builtin

Algumas fun√ß√µes s√£o embutidas como `println`. Aqui est√° a lista completa:

```v ignore
fn print(s string) // print anything on sdtout
fn println(s string) // print anything and a newline on sdtout

fn eprint(s string) // same as print(), but use stderr
fn eprintln(s string) // same as println(), but use stderr

fn exit(code int) // terminate the program with a custom error code
fn panic(s string) // print a message and backtraces on stderr, and terminate the program with error code 1
fn print_backtrace() // print backtraces on stderr
```

`println` √© uma fun√ß√£o embutida simples, mas poderosa, que pode imprimir qualquer coisa:
strings, n√∫meros, matrizes, mapas, estruturas.

```v
struct User {
	name string
	age  int
}

println(1) // "1"
println('hi') // "hi"
println([1, 2, 3]) // "[1, 2, 3]"
println(User{ name: 'Bob', age: 20 }) // "User{name:'Bob', age:20}"
```

<a id='custom-print-of-types' />

## Impress√£o de tipos personalizados

Se voc√™ deseja definir um valor de impress√£o personalizado para o seu tipo, basta definir um
M√©todo `.str() string`:

```v
struct Color {
	r int
	g int
	b int
}

pub fn (c Color) str() string {
	return '{$c.r, $c.g, $c.b}'
}

red := Color{
	r: 255
	g: 0
	b: 0
}
println(red)
```

## M√≥dulos

Cada arquivo na raiz de uma pasta faz parte do mesmo m√≥dulo.
Programas simples n√£o precisam especificar o nome do m√≥dulo; nesse caso, o padr√£o √© 'main'.

V √© uma linguagem muito modular. A cria√ß√£o de m√≥dulos reutiliz√°veis ‚Äã‚Äã√© incentivada e
muito f√°cil de fazer.
Para criar um novo m√≥dulo, crie um diret√≥rio com o nome do seu m√≥dulo contendo
Arquivos .v com c√≥digo:

```shell
cd ~/code/modules
mkdir mymodule
vim mymodule/myfile.v
```
```v failcompile
// myfile.v
module mymodule

// Para exportar uma fun√ß√£o, temos que usar `pub`
pub fn say_hi() {
	println('hello from mymodule!')
}
```

Agora voc√™ pode usar `mymodule` em seu c√≥digo:

```v failcompile
import mymodule

fn main() {
	mymodule.say_hi()
}
```

* Os nomes dos m√≥dulos devem ser curtos, com menos de 10 caracteres.
* Os nomes dos m√≥dulos devem usar `snake_case`.
* Importa√ß√µes circulares n√£o s√£o permitidas.
* Voc√™ pode ter quantos arquivos .v quiser em um m√≥dulo.
* Voc√™ pode criar m√≥dulos em qualquer lugar.
* Todos os m√≥dulos s√£o compilados estaticamente em um √∫nico execut√°vel.

### Fun√ß√µes `init`

Se voc√™ quiser que um m√≥dulo chame automaticamente
algum c√≥digo de configura√ß√£o/inicializa√ß√£o quando for importado,
voc√™ pode usar uma fun√ß√£o do m√≥dulo `init`:

```v
fn init() {
	// your setup code here ...
}
```

A fun√ß√£o `init` n√£o pode ser p√∫blica - ela ser√° chamada automaticamente.
Este recurso √© particularmente √∫til para inicializar uma biblioteca C.

### M√≥dulo Gerenciador de pacote

Brevemente:

```powershell
v [module option] [param]
```

###### module options:

```
   install           Install a module from VPM.
   remove            Remove a module that was installed from VPM.
   search            Search for a module from VPM.
   update            Update an installed module from VPM.
   upgrade           Upgrade all the outdated modules.
   list              List all installed modules.
   outdated          Show installed modules that need updates.
```

Leia mais:

Voc√™ tamb√©m pode instalar m√≥dulos j√° criados por outra pessoa com [VPM](https://vpm.vlang.io/):
```powershell
v install [module]
```
###### Exemplo:
```powershell
v install ui
```

Removendo um m√≥dulo com V:

```powershell
v remove [module]
```
###### Exemplo:
```powershell
v remove ui
```

Atualizando um m√≥dulo instalado do [VPM](https://vpm.vlang.io/):

```powershell
v update [module]
```
###### Exemplo:
```powershell
v update ui
```

Ou voc√™ pode atualizar todos os seus m√≥dulos:
```powershell
v update
```

Para ver todos os m√≥dulos instalados, pode-se usar:

```powershell
v list
```
###### Exemplo
```powershell
> v list
Installed modules:
  markdown
  ui
```

Para ver todos os m√≥dulos instalados e que est√£o desatualizados, pode-se usar:
outdated          Show installed modules that need updates.
```powershell
v outdated
```
###### Exemplo
```powershell
> v outdated
Modules are up to date.
```

Voc√™ tamb√©m pode adicionar seu m√≥dulo ao VPM seguindo as instru√ß√µes no site https://vpm.vlang.io/new

## Tipos 2

### Interfaces

```v
struct Dog {
	breed string
}

struct Cat {
	breed string
}

fn (d Dog) speak() string {
	return 'woof'
}

fn (c Cat) speak() string {
	return 'meow'
}

// ao contr√°rio de Go e TypeScript, as interfaces de V podem definir campos, n√£o apenas m√©todos.
interface Speaker {
	breed string
	speak() string
}

dog := Dog{'Leonberger'}
cat := Cat{'Siamese'}

mut arr := []Speaker{}
arr << dog
arr << cat
for item in arr {
	println('a $item.breed says: $item.speak()')
}
```

Um tipo implementa uma interface implementando seus m√©todos e campos.
N√£o h√° declara√ß√£o expl√≠cita de inten√ß√£o, nem palavra-chave "implements".

#### Casting uma interface

Podemos testar o tipo subjacente de uma interface usando operadores de convers√£o din√¢mica:
```v oksyntax
interface Something {}

fn announce(s Something) {
	if s is Dog {
		println('a $s.breed dog') // `s` √© automaticamente "casteada" para `Dog` (smart cast)
	} else if s is Cat {
		println('a $s.breed cat')
	} else {
		println('something else')
	}
}
```
Para mais informa√ß√µes, veja [Dynamic casts](#dynamic-casts).

#### Defini√ß√µes de m√©todos de interface

Tamb√©m ao contr√°rio do Go, uma interface pode implementar um m√©todo.
Esses m√©todos n√£o s√£o implementados por structs que implementam essa interface.

Quando uma estrutura √© envolvida em uma interface que implementou um m√©todo
com o mesmo nome de um implementado por esta estrutura, apenas o m√©todo
implementado na interface √© chamado.

```v
struct Cat {}

fn (c Cat) speak() string {
	return 'meow!'
}

interface Adoptable {}

fn (a Adoptable) speak() string {
	return 'adopt me!'
}

fn new_adoptable() Adoptable {
	return Cat{}
}

fn main() {
	cat := Cat{}
	assert cat.speak() == 'meow!'
	a := new_adoptable()
	assert a.speak() == 'adopt me!'
	if a is Cat {
		println(a.speak()) // meow!
	}
}
```

### Enums

```v
enum Color {
	red
	green
	blue
}

mut color := Color.red
// V sabe que `color` √© uma` Color`. N√£o h√° necessidade de usar `color = Color.green` aqui.
color = .green
println(color) // "green"
match color {
	.red { println('the color was red') }
	.green { println('the color was green') }
	.blue { println('the color was blue') }
}
```

A correspond√™ncia de enum deve ser exaustiva ou ter um branch `else`.
Isso garante que, se um novo campo enum for adicionado,
ele ser√° tratado em todos os lugares do c√≥digo.

Os campos Enum n√£o podem reutilizar palavras-chave reservadas.
No entanto, palavras-chave reservadas podem ter escape
com um @.

```v
enum Color {
	@none
	red
	green
	blue
}

color := Color.@none
println(color)
```

Inteiros podem ser atribu√≠dos a campos enum.

```v
enum Grocery {
	apple
	orange = 5
	pear
}

g1 := int(Grocery.apple)
g2 := int(Grocery.orange)
g3 := int(Grocery.pear)
println('Grocery IDs: $g1, $g2, $g3')
```

Output: `Grocery IDs: 0, 5, 6`.

Opera√ß√µes n√£o s√£o permitidas em vari√°veis ‚Äã‚Äãenum;
eles devem ser explicitamente convertidos em `int`.

### Sum types

Uma inst√¢ncia do tipo sum pode conter um valor de v√°rios tipos diferentes.
Use a palavra-chave `type` para declarar um tipo de sum:

```v
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

sum := World(Moon{})
assert sum.type_name() == 'Moon'
println(sum)
```
O m√©todo embutido `type_name` retorna o nome do atualmente mantido
modelo.

Com os tipos de sum, voc√™ pode construir estruturas recursivas
e escrever um c√≥digo conciso, mas poderoso, nelas.
```v
// V's binary tree
struct Empty {}

struct Node {
	value f64
	left  Tree
	right Tree
}

type Tree = Empty | Node

// sum up all node values
fn sum(tree Tree) f64 {
	return match tree {
		Empty { f64(0) } // TODO: as match gets smarter just remove f64()
		Node { tree.value + sum(tree.left) + sum(tree.right) }
	}
}

fn main() {
	left := Node{0.2, Empty{}, Empty{}}
	right := Node{0.3, Empty{}, Node{0.4, Empty{}, Empty{}}}
	tree := Node{0.5, left, right}
	println(sum(tree)) // 0.2 + 0.3 + 0.4 + 0.5 = 1.4
}
```

#### Dynamic casts

Para verificar se uma inst√¢ncia de tipo de sum cont√©m um certo tipo, use `sum is Type`.
Para lan√ßar um tipo de sum para uma de suas variantes, voc√™ pode usar `sum as Type`:

```v
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

fn (m Mars) dust_storm() bool {
	return true
}

fn main() {
	mut w := World(Moon{})
	assert w is Moon
	w = Mars{}
	// use `as` to access the Mars instance
	mars := w as Mars
	if mars.dust_storm() {
		println('bad weather!')
	}
}
```

`as` ser√° panic se` w` n√£o contiver uma inst√¢ncia de `Mars`.
Uma maneira mais segura √© usar um elenco inteligente.

#### Smart casting

```v oksyntax
if w is Mars {
	assert typeof(w).name == 'Mars'
	if w.dust_storm() {
		println('bad weather!')
	}
}
```
`w` tem o tipo` Mars` dentro do corpo da instru√ß√£o `if`. Isso √©
conhecido como * digita√ß√£o sens√≠vel ao fluxo *.
Se `w` for um identificador mut√°vel,
n√£o seria seguro se o compilador o convertesse de maneira inteligente sem um aviso.
√â por isso que voc√™ deve declarar um `mut` antes da express√£o` is`:

```v ignore
if mut w is Mars {
	assert typeof(w).name == 'Mars'
	if w.dust_storm() {
		println('bad weather!')
	}
}
```
Caso contr√°rio, `w` manteria seu tipo original.
> Isso funciona para vari√°veis ‚Äã‚Äãsimples e express√µes complexas como `user.name`

#### Matching sum types

Voc√™ tamb√©m pode usar `match` para determinar a variante:

```v
struct Moon {}

struct Mars {}

struct Venus {}

type World = Mars | Moon | Venus

fn open_parachutes(n int) {
	println(n)
}

fn land(w World) {
	match w {
		Moon {} // no atmosphere
		Mars {
			// light atmosphere
			open_parachutes(3)
		}
		Venus {
			// heavy atmosphere
			open_parachutes(1)
		}
	}
}
```

`match` deve ter um padr√£o para cada variante ou ter um branch` else`.

```v ignore
struct Moon {}
struct Mars {}
struct Venus {}

type World = Moon | Mars | Venus

fn (m Moon) moon_walk() {}
fn (m Mars) shiver() {}
fn (v Venus) sweat() {}

fn pass_time(w World) {
    match w {
        // using the shadowed match variable, in this case `w` (smart cast)
        Moon { w.moon_walk() }
        Mars { w.shiver() }
        else {}
    }
}
```

### Type aliases

Para definir um novo tipo `NewType` como um alias para` ExistingType`,
fa√ßa `type NewType = ExistingType`. <br/>
Este √© um caso especial de declara√ß√£o [sum type](#sum-types).

### Option/Result types and error handling

Os tipos de op√ß√µes s√£o declarados com `?Type`:
```v
struct User {
	id   int
	name string
}

struct Repo {
	users []User
}

fn (r Repo) find_user_by_id(id int) ?User {
	for user in r.users {
		if user.id == id {
			// V automatically wraps this into an option type
			return user
		}
	}
	return error('User $id not found')
}

fn main() {
	repo := Repo{
		users: [User{1, 'Andrew'}, User{2, 'Bob'}, User{10, 'Charles'}]
	}
	user := repo.find_user_by_id(10) or { // Option types must be handled by `or` blocks
		return
	}
	println(user.id) // "10"
	println(user.name) // "Charles"
}
```

V combina `Option` e` Result` em um tipo,
ent√£o voc√™ n√£o precisa decidir qual usar.

A quantidade de trabalho necess√°ria para "atualizar"
uma fun√ß√£o para uma fun√ß√£o opcional √© m√≠nima;
voc√™ tem que adicionar um `?` ao ‚Äã‚Äãtipo de retorno
e retornar um erro quando algo der errado.

Se voc√™ n√£o precisa retornar uma mensagem de erro,
voc√™ pode simplesmente `return none`
(este √© um equivalente mais eficiente de `return error (" ")`).

Este √© o mecanismo principal para tratamento de erros em V.
Eles ainda s√£o valores, como em Go,
mas a vantagem √© que os erros n√£o podem ser resolvidos,
e trat√°-los √© muito menos prolixo.
Ao contr√°rio de outras linguagens, V n√£o lida com exce√ß√µes com blocos `throw/try/catch`.

`err` √© definido dentro de um bloco `or` e
√© definido para a string que a mensagem foi passada
para a fun√ß√£o `error()`. `err` estar√° vazio se `none` for retornado.

```v oksyntax
user := repo.find_user_by_id(7) or {
	println(err) // "User 7 not found"
	return
}
```

### Handling optionals

Existem quatro maneiras de lidar com um opcional.
O primeiro m√©todo √© propagar o erro:

```v
import net.http

fn f(url string) ?string {
	resp := http.get(url) ?
	return resp.text
}
```

`http.get` retorna `?http.Response`. Porque `?` Segue a chamada, o
o erro ser√° propagado para o chamador de `f`. Ao usar `?` ap√≥s uma
chamada de fun√ß√£o produzindo um opcional, a fun√ß√£o envolvente deve retornar
um opcional tamb√©m. Se a propaga√ß√£o de erro for usada no `main()`
em vez disso, ele entrar√° em 'panic', uma vez que o erro n√£o pode ser propagado
mais longe.

O corpo de `f` √© essencialmente uma vers√£o condensada de:

```v ignore
    resp := http.get(url) or { return err }
    return resp.text
```

---
O segundo m√©todo √© interromper a execu√ß√£o mais cedo:

```v oksyntax
user := repo.find_user_by_id(7) or { return }
```

Aqui, voc√™ pode chamar `panic()` ou `exit()`, o que interromper√° a execu√ß√£o de
todo o programa ou usar uma instru√ß√£o de fluxo de controle (`return`,` break`, `continue`, etc)
para quebrar do bloco atual.
Observe que `break` e` continue` s√≥ podem ser usados ‚Äã‚Äãdentro de um loop `for`.

V n√£o tem uma maneira de for√ßar "unwrap" um opcional (como outras linguagens fazem,
por exemplo, o `unwrap()` do Rust ou o `!` do Swift). Para fazer isso, use `ou {panic (err.msg)}`.

---
O terceiro m√©todo √© fornecer um valor padr√£o no final do bloco `or`.
Em caso de erro, esse valor seria atribu√≠do em vez disso,
portanto, deve ter o mesmo tipo que o conte√∫do da `Option` sendo tratada.

```v
fn do_something(s string) ?string {
	if s == 'foo' {
		return 'foo'
	}
	return error('invalid string') // Could be `return none` as well
}

a := do_something('foo') or { 'default' } // a ser√° 'foo'
b := do_something('bar') or { 'default' } // b ser√° 'default'
println(a)
println(b)
```

---
O quarto m√©todo √© usar desdobramento `if`:

```v
import net.http

if resp := http.get('https://google.com') {
	println(resp.text) // resp is a http.Response, not an optional
} else {
	println(err)
}
```
Acima, `http.get` retorna um `?Http.Response`. `resp` est√° apenas no escopo para o primeiro
ramo `if`. `err` est√° apenas no escopo do branch `else`.

## Generics

```v wip

struct Repo<T> {
    db DB
}

struct User {
	id   int
	name string
}

struct Post {
	id   int
	user_id int
	title string
	body string
}

fn new_repo<T>(db DB) Repo<T> {
    return Repo<T>{db: db}
}

// Esta √© uma fun√ß√£o gen√©rica. V ir√° ger√°-lo para cada tipo com o qual √© usado.
fn (r Repo<T>) find_by_id(id int) ?T {
    table_name := T.name // neste exemplo, obter o nome do tipo nos d√° o nome da tabela
    return r.db.query_one<T>('select * from $table_name where id = ?', id)
}

db := new_db()
users_repo := new_repo<User>(db) // returns Repo<User>
posts_repo := new_repo<Post>(db) // returns Repo<Post>
user := users_repo.find_by_id(1)? // find_by_id<User>
post := posts_repo.find_by_id(1)? // find_by_id<Post>
```

Atualmente, as defini√ß√µes de fun√ß√µes gen√©ricas devem declarar seus par√¢metros de tipo, mas no
futuro V ir√° inferir par√¢metros de tipo gen√©rico a partir de nomes de tipo de uma √∫nica letra em
tipos de par√¢metro de tempo de execu√ß√£o. √â por isso que `find_by_id` pode omitir `<T>`, porque o
o argumento do receptor `r` usa um tipo gen√©rico `T`.

Outro exemplo:
```v
fn compare<T>(a T, b T) int {
	if a < b {
		return -1
	}
	if a > b {
		return 1
	}
	return 0
}

// compare<int>
println(compare(1, 0)) // Outputs: 1
println(compare(1, 1)) //          0
println(compare(1, 2)) //         -1
// compare<string>
println(compare('1', '0')) // Outputs: 1
println(compare('1', '1')) //          0
println(compare('1', '2')) //         -1
// compare<f64>
println(compare(1.1, 1.0)) // Outputs: 1
println(compare(1.1, 1.1)) //          0
println(compare(1.1, 1.2)) //         -1
```


## Concorr√™ncia
### Spawning Concurrent Tasks
O modelo de simultaneidade de V √© muito semelhante ao de Go.
Para executar `foo()`, simultaneamente em
um thread diferente, basta cham√°-lo com `go foo()`:

```v
import math

fn p(a f64, b f64) { // fun√ß√£o comum sem valor de retorno
	c := math.sqrt(a * a + b * b)
	println(c)
}

fn main() {
	go p(3, 4)
	// p ser√° rodado em uma thread paralela
}
```

√Äs vezes, √© necess√°rio esperar at√© que uma thread paralela termine. Isso pode
ser feito atribuindo um * identificador * √† thread iniciada e chamando o m√©todo `wait()`
para este identificador mais tarde:

```v
import math

fn p(a f64, b f64) { // fun√ß√£o comum sem valor de retorno
	c := math.sqrt(a * a + b * b)
	println(c) // prints `5`
}

fn main() {
	h := go p(3, 4)
	// p() roda em thread paralela
	h.wait()
	// p() definitivamente terminou
}
```

Essa abordagem tamb√©m pode ser usada para obter um
valor de retorno de uma fun√ß√£o que √© executada em uma
thread paralela. N√£o h√° necessidade de modificar
a pr√≥pria fun√ß√£o para poder cham√°-la
simultaneamente.

```v
import math { sqrt }

fn get_hypot(a f64, b f64) f64 { // fun√ß√£o comum retornando um valor
	c := sqrt(a * a + b * b)
	return c
}

fn main() {
	g := go get_hypot(54.06, 2.08) // spawn thread
	h1 := get_hypot(2.32, 16.74) //   Faz outro c√°lculo aqui
	h2 := g.wait() //                 pega o resultado da thread spawned (get_hypot(54.06, 2.08))
	println('Results: $h1, $h2') //   prints `Results: 16.9, 54.1`
}
```

Se houver um grande n√∫mero de tasks, pode ser mais f√°cil gerenci√°-las
usando uma s√©rie de threads.

```v
import time

fn task(id int, duration int) {
	println('task $id begin')
	time.sleep(duration * time.millisecond)
	println('task $id end')
}

fn main() {
	mut threads := []thread{}
	threads << go task(1, 500)
	threads << go task(2, 900)
	threads << go task(3, 100)
	threads.wait()
	println('done')
}

// Output:
// task 1 begin
// task 2 begin
// task 3 begin
// task 3 end
// task 1 end
// task 2 end
// done
```

Al√©m disso, para threads que retornam o mesmo tipo, chamando `wait()`
no array de thread retornar√° todos os valores calculados.

```v
fn expensive_computing(i int) int {
	return i * i
}

fn main() {
	mut threads := []thread int{}
	for i in 1 .. 10 {
		threads << go expensive_computing(i)
	}
	// Junta todas as tasks
	r := threads.wait()
	println('All jobs finished: $r')
}

// Output: All jobs finished: [1, 4, 9, 16, 25, 36, 49, 64, 81]
```

### Channels
Os canais s√£o a forma preferida de comunica√ß√£o entre as corrotinas.
Os canais de V funcionam basicamente como
aqueles em Go. Voc√™ pode empurrar objetos para um canal
em uma extremidade e estourar objetos na outra extremidade.
Os canais podem ser armazenados em buffer ou sem buffer
e √© poss√≠vel `selecionar` a partir de v√°rios canais.

#### Syntax and Usage
Channels t√™m o tipo `chan objtype`. Um comprimento de buffer
opcional pode ser especificado como a propriedade `cap`
na declara√ß√£o:

```v
ch := chan int{} // unbuffered - "synchronous"
ch2 := chan f64{cap: 100} // buffer length 100
```

Os channles n√£o precisam ser declarados como `mut`.
O comprimento do buffer n√£o faz parte do tipo, mas
uma propriedade do objeto de canal individual.
Os canais podem ser passados ‚Äã‚Äãpara corrotinas como
vari√°veis:

```v
fn f(ch chan int) {
	// ...
}

fn main() {
	ch := chan int{}
	go f(ch)
	// ...
}
```

Os objetos podem ser enviados para os canais
usando o operador '<-'. O mesmo operador pode ser usado para
objetos pop do outro lado:

```v
// crie canais em buffer para que o push n√£o
// bloqueie (se houver espaco no buffer)
ch := chan int{cap: 1}
ch2 := chan f64{cap: 1}
n := 5
// push
ch <- n
ch2 <- 7.3
mut y := f64(0.0)
m := <-ch // pop criando nova vari√°vel
y = <-ch2 // pop dentro de uma vari√°vel existente
```

Um canal pode ser fechado para indicar que nenhum
outro objeto pode ser empurrado. Qualquer tentativa de
fazer isso, resultar√° em um 'panic' de tempo de execu√ß√£o
(com exce√ß√£o de `select` e
`try_push()` - veja abaixo). As tentativas de estourar
retornar√£o imediatamente se o
o canal associado foi fechado e o buffer est√° vazio.
Esta situa√ß√£o pode ser
manipulado usando um branch ou (consulte [Handling optionals](#handling-optionals)).

```v wip
ch := chan int{}
ch2 := chan f64{}
// ...
ch.close()
// ...
m := <-ch or {
    println('channel has been closed')
}

// propagate error
y := <-ch2 ?
```

#### Channel Select

O comando `select` permite monitorar v√°rios canais ao mesmo tempo
sem carga de CPU percept√≠vel. Consiste em uma lista
de poss√≠veis transfer√™ncias e filiais associadas
de declara√ß√µes - semelhante ao comando [match](#match):
```v wip
import time
fn main () {
  c := chan f64{}
  ch := chan f64{}
  ch2 := chan f64{}
  ch3 := chan f64{}
  mut b := 0.0
  // ...
  select {
    a := <-ch {
        // fa√ßa algo com `a`
    }
    b = <-ch2 {
        // fa√ßa algo com a vari√°vel pr√©-declarada `b`
    }
    ch3 <- c {
        // fa√ßa algo se `c` foi enviado
    }
    > 500 * time.millisecond {
        // fa√ßa algo se nenhum channel ficou pronto dentro de 0.5s
    }
  }
}
```

O branch de tempo limite √© opcional. Se estiver ausente,
`select` espera por um per√≠odo ilimitado de tempo.
Tamb√©m √© poss√≠vel proceder imediatamente se nenhum canal
estiver pronto no momento em que `select` √© chamado
adicionando um branch `else {...}`. `else` e `> timeout`
s√£o mutuamente exclusivos.

O comando `select` pode ser usado como uma * express√£o * do tipo` bool`
que se torna `falso` se todos os canais forem fechados:
```v wip
if select {
    ch <- a {
        // ...
    }
} {
    // channel foi aberto
} else {
    // channel est√° fechado
}
```

#### Special Channel Features

Para fins especiais, existem algumas propriedades e m√©todos integrados:
```v
struct Abc {
	x int
}

a := 2.13
ch := chan f64{}
res := ch.try_push(a) // try to perform `ch <- a`
println(res)
l := ch.len // n√∫mero de elementos na fila
c := ch.cap // tamanho m√°ximo da fila
is_closed := ch.closed // bool flag - `ch` foi fechado?
println(l)
println(c)
mut b := Abc{}
ch2 := chan Abc{}
res2 := ch2.try_pop(mut b) // try to perform `b = <-ch2`
```

Os m√©todos `try_push/pop()` retornar√£o imediatamente com um dos resultados
`.success`,` .not_ready` ou `.closed` - depende se o objeto foi transferido ou
a raz√£o pela qual n√£o.
O uso desses m√©todos e propriedades na produ√ß√£o n√£o √© recomendado -
algoritmos baseados neles est√£o frequentemente sujeitos
a condi√ß√µes de corrida. Especialmente `.len` e
`.closed` n√£o deve ser usado para tomar decis√µes.
Use ramos `or`, propaga√ß√£o de erro ou `select`
em vez disso (veja [Syntax and Usage](#syntax-and-usage)
e [Channel Select](#channel-select) acima).

### Objetos Compartilhados

Os dados podem ser trocados entre uma co-rotina e
o thread de chamada por meio de uma vari√°vel compartilhada.
Essas vari√°veis ‚Äã‚Äãdevem ser criadas como `shared` e
passadas para a co-rotina como tal.
A `struct` subjacente cont√©m um * mutex * oculto
que permite o bloqueio de acesso simult√¢neo
usando `rlock` para somente leitura e `lock`
para acesso de leitura/grava√ß√£o.

```v
struct St {
mut:
	x int // dado a ser compartilhado
}

fn (shared b St) g() {
	lock b {
		// read/modify/write b.x
	}
}

fn main() {
	shared a := St{
		x: 10
	}
	go a.g()
	// ...
	rlock a {
		// read a.x
	}
}
```
Vari√°veis 'shared' devem ser 'structs', 'arrays' or 'maps'.

## Decoding JSON

```v
import json

struct Foo {
	x int
}

struct User {
	name string
	age  int
	// Use o atributo `skip` para pular certos campos
	foo Foo [skip]
	// Se o nome do campo est√° diferente no JSON, ele pode ser especificado
	last_name string [json: lastName]
}

data := '{ "name": "Frodo", "lastName": "Baggins", "age": 25 }'
user := json.decode(User, data) or {
	eprintln('Failed to decode json')
	return
}
println(user.name)
println(user.last_name)
println(user.age)
// Voc√™ tamb√©m pode decodificar JSON arrays:
sfoos := '[{"x":123},{"x":456}]'
foos := json.decode([]Foo, sfoos) ?
println(foos[0].x)
println(foos[1].x)
```

Devido √† natureza onipresente do JSON, o suporte
para ele √© integrado diretamente no V.

A fun√ß√£o `json.decode` leva dois argumentos:
o primeiro √© o tipo em que o valor JSON deve ser decodificado e
a segunda √© uma string contendo os dados JSON.

V gera c√≥digo para codifica√ß√£o e decodifica√ß√£o JSON.
Nenhuma 'runtime reflection' √© usada. Isso resulta
em um desempenho muito melhor.

## Testing

### Asserts

```v
fn foo(mut v []int) {
	v[0] = 1
}

mut v := [20]
foo(mut v)
assert v[0] < 4
```
Uma instru√ß√£o `assert` verifica se sua express√£o √©
avaliada como `true`. Se uma afirma√ß√£o falhar,
o programa ser√° abortado. Asserts devem ser usados
‚Äã‚Äãapenas para detectar erros de programa√ß√£o. Quando um
declara√ß√£o falha, √© relatado para * stderr *, e os
valores em cada lado de um operador de compara√ß√£o
(como `<`, `==`) ser√° impresso quando poss√≠vel.
Isso √© √∫til para encontrar facilmente um
valor inesperado. As declara√ß√µes assert podem
ser usadas em qualquer fun√ß√£o.

### Test files

```v
// hello.v
module main

fn hello() string {
	return 'Hello world'
}

fn main() {
	println(hello())
}
```

```v failcompile
module main

// hello_test.v
fn test_hello() {
	assert hello() == 'Hello world'
}
```
Para executar o teste acima, use `v hello_test.v`.
Isso ir√° verificar se a fun√ß√£o `hello` est√°
produzindo a sa√≠da correta.
V executa todas as fun√ß√µes de teste no arquivo.

* Todas as fun√ß√µes de teste devem estar dentro
de um arquivo de teste cujo nome termina em `_test.v`.
* Os nomes das fun√ß√µes de teste devem come√ßar com
`test_` para marc√°-los para execu√ß√£o.
* As fun√ß√µes normais tamb√©m podem ser definidas em
arquivos de teste e devem ser chamadas manualmente. Outros
  s√≠mbolos tamb√©m podem ser definidos em arquivos
  de teste, por exemplo tipos.
* Existem dois tipos de testes: externos e internos.
* Os testes internos devem * declarar * seu m√≥dulo,
assim como todos os outros .v
arquivos do mesmo m√≥dulo. Os testes internos podem
at√© chamar fun√ß√µes privadas em
o mesmo m√≥dulo.
* Os testes externos devem * importar * os m√≥dulos
que eles testam. Eles n√£o
t√™m acesso √†s fun√ß√µes/tipos privados dos m√≥dulos.
Eles podem testar apenas
a API externa/p√∫blica que um m√≥dulo fornece.

No exemplo acima, `test_hello` √© um teste interno,
que pode chamar
a fun√ß√£o privada `hello()` porque `hello_test.v`
tem `module main`,
assim como `hello.v`, ou seja, ambos fazem parte
do mesmo m√≥dulo. Observe tamb√©m que
uma vez que `module main` √© um m√≥dulo regular
como os outros, os testes internos podem
tamb√©m ser usado para testar fun√ß√µes privadas
nos arquivos .v do seu programa principal.

Voc√™ tamb√©m pode definir fun√ß√µes de teste
especiais em um arquivo de teste:
* `testsuite_begin` que ser√° executado * antes *
de todas as outras fun√ß√µes de teste.
* `testsuite_end` que ser√° executado * ap√≥s *
todas as outras fun√ß√µes de teste.

Se uma fun√ß√£o de teste tiver um tipo de retorno
de erro, qualquer erro propagado falhar√° no teste:

```
import strconv

fn test_atoi() ? {
	assert strconv.atoi('1') ? == 1
	assert strconv.atoi('one') ? == 1 // teste ir√° falhar
}
```

#### Rodando os testes

Para executar fun√ß√µes de teste em um arquivo
de teste individual, use `v foo_test.v`.

Para testar um m√≥dulo inteiro, use `v test mymodule`.
Voc√™ tamb√©m pode usar `v test .` para testar
tudo dentro de sua pasta atual (e subpastas).
Voc√™ pode passar a op√ß√£o `-stats`
para ver mais detalhes sobre os testes individuais executados.

## Gerenciamento de Mem√≥ria

V evita fazer aloca√ß√µes desnecess√°rias em
primeiro lugar, usando tipos de valor,
buffers de string, promovendo um estilo
de c√≥digo simples e livre de abstra√ß√£o.

A maioria dos objetos (~ 90-100%) s√£o
liberados pelo motor autofree de V: o compilador insere
chamadas gratuitas necess√°rias automaticamente
durante a compila√ß√£o. Pequena porcentagem restante
de objetos √© liberado por meio da contagem de refer√™ncia.

O desenvolvedor n√£o precisa alterar nada
em seu c√≥digo. "Simplesmente funciona", como em
Python, Go ou Java, exceto que n√£o h√° GC
pesado rastreando tudo ou RC caro para
cada objeto.

### Controle

Voc√™ pode tirar vantagem do motor autofree
do V e definir um m√©todo `free()` nos
tipos de dados customizados:

```v
struct MyType {}

[unsafe]
fn (data &MyType) free() {
	// ...
}
```

Assim como o compilador libera tipos de dados C
com `free()` do C, ele ir√° inserir estaticamente
`free()` chamadas para seu tipo de dado no final
do tempo de vida de cada vari√°vel.

Para desenvolvedores que desejam ter mais controle
de baixo n√≠vel, o autofree pode ser desabilitado com
`-manualfree`, ou adicionando um` [manualfree]` em
cada fun√ß√£o que deseja gerenciar sua
mem√≥ria manualmente. (Veja [attributes](#attributes))._

Nota: neste momento, o autofree est√° escondido atr√°s
da flag '-autofree'. Ser√° habilitado por
padr√£o em V 0.3. Se o autofree n√£o for usado,
os programas V vazar√£o mem√≥ria._

### Exemplos

```v
import strings

fn draw_text(s string, x int, y int) {
	// ...
}

fn draw_scene() {
	// ...
	name1 := 'abc'
	name2 := 'def ghi'
	draw_text('hello $name1', 10, 10)
	draw_text('hello $name2', 100, 10)
	draw_text(strings.repeat(`X`, 10000), 10, 50)
	// ...
}
```

As strings n√£o escapam de `draw_text`,
ent√£o elas s√£o limpas quando
a fun√ß√£o sai.

Na verdade, com o sinalizador `-prealloc`,
as duas primeiras chamadas n√£o resultar√£o em nenhuma aloca√ß√£o.
Essas duas strings s√£o pequenas,
ent√£o V usar√° um buffer pr√©-alocado para elas.

```v
struct User {
	name string
}

fn test() []int {
	number := 7 // stack variable
	user := User{} // struct alocado na stack
	numbers := [1, 2, 3] // array alocatodo na heap, ser√° liberado quando a fun√ß√£o sair
	println(number)
	println(user)
	println(numbers)
	numbers2 := [4, 5, 6] // array que est√° sendo devolvido, n√£o ser√° liberado aqui
	return numbers2
}
```

## ORM

(Ainda est√° em estado alfa)

V tem um ORM embutido (mapeamento objeto-relacional)
que suporta SQLite, MySQL e Postgres,
mas em breve ter√° suporte para MS SQL e Oracle.

ORM da V oferece uma s√©rie de benef√≠cios:

- Uma sintaxe para todos os dialetos SQL.
(Migrar entre bancos de dados se torna muito mais f√°cil.)
- As consultas s√£o constru√≠das usando a sintaxe de V.
(N√£o h√° necessidade de aprender outra sintaxe.)
- Seguran√ßa.
(Todas as consultas s√£o automaticamente higienizadas para evitar inje√ß√£o de SQL.)
- Compilar verifica√ß√µes de tempo.
(Isso evita erros de digita√ß√£o que s√≥ podem ser detectados durante o tempo de execu√ß√£o.)
- Legibilidade e simplicidade.
(Voc√™ n√£o precisa analisar manualmente os resultados de uma consulta e
    em seguida, construir manualmente os objetos a partir dos resultados analisados.)

```v
import sqlite

struct Customer {
	// nome da struct tem que ser o mesmo que o da nome da tabela (por enquanto)
	id        int    [primary; sql: serial] // um campo chamdado 'id' de tipo integer deve ser o primeiro campo
	name      string [nonull]
	nr_orders int
	country   string [nonull]
}

db := sqlite.connect('customers.db') ?

// voc√™ pode criar tabelas
// CREATE TABLE IF NOT EXISTS `Customer` (`id` INTEGER PRIMARY KEY, `name` TEXT NOT NULL, `nr_orders` INTEGER, `country` TEXT NOT NULL)
sql db {
	create table Customer
}

// select count(*) from Customer
nr_customers := sql db {
	select count from Customer
}
println('number of all customers: $nr_customers')
// V syntax pode ser usada para construir queries
uk_customers := sql db {
	select from Customer where country == 'uk' && nr_orders > 0
}
println(uk_customers.len)
for customer in uk_customers {
	println('$customer.id - $customer.name')
}
// adicionando `limit 1`, dizemos para V que ter√°, somente, um objeto
customer := sql db {
	select from Customer where id == 1 limit 1
}
println('$customer.id - $customer.name')
// insert um novo customer
new_customer := Customer{
	name: 'Bob'
	nr_orders: 10
}
sql db {
	insert new_customer into Customer
}
```

Para mais exemplos e docs,
veja <a href='https://github.com/vlang/v/tree/master/vlib/orm'>vlib/orm</a>.

## Escrevendo a Documenta√ß√£o

A forma como funciona √© muito semelhante ao Go.
√â muito simples: n√£o h√° necessidade de
escrever a documenta√ß√£o separadamente para o seu c√≥digo,
vdoc ir√° ger√°-lo a partir de docstrings no c√≥digo-fonte.

A documenta√ß√£o para cada fun√ß√£o/tipo/const
deve ser colocada logo antes da declara√ß√£o:

```v
// clearall clears all bits in the array
fn clearall() {
}
```

O coment√°rio deve come√ßar com o nome da defini√ß√£o.

√Äs vezes, uma linha n√£o √© suficiente para explicar
o que uma fun√ß√£o faz, nesse caso, os coment√°rios devem
alcan√ßar a fun√ß√£o documentada usando coment√°rios
de uma √∫nica linha:

```v
// copy_all recursively copies all elements of the array by their value,
// if `dupes` is false all duplicate values are eliminated in the process.
fn copy_all(dupes bool) {
	// ...
}
```

Por conven√ß√£o, √© prefer√≠vel que os coment√°rios
sejam escritos no * tempo presente *.

Uma vis√£o geral do m√≥dulo deve ser colocada
no primeiro coment√°rio logo ap√≥s o nome do m√≥dulo.

Para gerar a documenta√ß√£o, use o vdoc,
por exemplo `v doc net.http`.

## Ferramentas

### v fmt

Voc√™ n√£o precisa se preocupar em formatar
seu c√≥digo ou definir diretrizes de estilo.
`v fmt` cuida disso:

```shell
v fmt file.v
```

√â recomendado configurar seu editor,
de forma que `v fmt -w` execute a cada salvamento.
Uma execu√ß√£o vfmt geralmente √© bem barata (leva < 30ms).

Sempre execute `v fmt -w file.v` antes de subir seu c√≥digo

### Profiling

V tem um bom suporte para tra√ßar o perfil
de seus programas: `v -profile profile.txt run file.v`
Isso produzir√° um arquivo profile.txt, que voc√™ pode analisar.

O arquivo profile.txt gerado ter√° linhas com 4 colunas:
a) quantas vezes uma fun√ß√£o foi chamada
b) quanto tempo no total uma fun√ß√£o levou (em ms)
c) quanto tempo, em m√©dia, uma chamada para uma fun√ß√£o demorou (em ns)
d) o nome da fun√ß√£o v

Voc√™ pode classificar na coluna 3 (tempo m√©dio por fun√ß√£o) usando:
`sort -n -k3 profile.txt | tail`

Voc√™ tamb√©m pode usar cron√¥metros para medir
apenas partes do seu c√≥digo explicitamente:
```v
import time

fn main() {
	sw := time.new_stopwatch({})
	println('Hello world')
	println('Greeting the world took: ${sw.elapsed().nanoseconds()}ns')
}
```

# T√≥picos Avan√ßados

## Dumping expressions at runtime
Voc√™ pode copiar/rastrear o valor de qualquer express√£o V usando `dump (expr)`.
Por exemplo, salve este exemplo de c√≥digo como `factorial.v` e execute-o com
`v run factorial.v`:
```v
fn factorial(n u32) u32 {
	if dump(n <= 1) {
		return dump(1)
	}
	return dump(n * factorial(n - 1))
}

fn main() {
	println(factorial(5))
}
```
Voc√™ vai ter:
```
[factorial.v:2] n <= 1: false
[factorial.v:2] n <= 1: false
[factorial.v:2] n <= 1: false
[factorial.v:2] n <= 1: false
[factorial.v:2] n <= 1: true
[factorial.v:3] 1: 1
[factorial.v:5] n * factorial(n - 1): 2
[factorial.v:5] n * factorial(n - 1): 6
[factorial.v:5] n * factorial(n - 1): 24
[factorial.v:5] n * factorial(n - 1): 120
120
```
Observe que `dump(expr)` rastrear√° tanto a localiza√ß√£o da fonte,
a pr√≥pria express√£o e o valor da express√£o.

## Memory-unsafe code

√Äs vezes, para efici√™ncia,
voc√™ pode querer escrever um c√≥digo de baixo n√≠vel que pode potencialmente
corromper a mem√≥ria ou ser
vulner√°vel a falhas de seguran√ßa. V suporta escrever esse c√≥digo,
mas n√£o por padr√£o.

V requer que quaisquer opera√ß√µes potencialmente
inseguras para a mem√≥ria sejam marcadas intencionalmente.
Marc√°-los tamb√©m indica para quem est√° lendo o c√≥digo que pode haver
viola√ß√µes de seguran√ßa de mem√≥ria se houver um erro.

Exemplos de opera√ß√µes potencialmente inseguras para a mem√≥ria s√£o:

* Pointer aritm√©tica
* Indexa√ß√£o de ponteiro
* Convers√£o para ponteiro de um tipo incompat√≠vel
* Chamar certas fun√ß√µes C, por exemplo `free`,` strlen` e `strncmp`.

Para marcar opera√ß√µes potencialmente
inseguras para a mem√≥ria, coloque-as em um bloco `unsafe`:

```v wip
// alocar 2 bytes n√£o inicializados e retornar uma refer√™ncia a eles
mut p := unsafe { malloc(2) }
p[0] = `h` // Error: pointer indexing is only allowed in `unsafe` blocks
unsafe {
    p[0] = `h` // OK
    p[1] = `i`
}
p++ // Error: pointer arithmetic is only allowed in `unsafe` blocks
unsafe {
    p++ // OK
}
assert *p == `i`
```

A pr√°tica recomendada √© evitar colocar
express√µes seguras de mem√≥ria dentro de um bloco `unsafe`,
para que o motivo do uso de `unsafe`
seja o mais claro poss√≠vel. Geralmente qualquer c√≥digo
que voc√™ acha que √© seguro para a
mem√≥ria n√£o deve estar dentro de um bloco `unsafe`, ent√£o o compilador
pode verificar isso.

Se voc√™ suspeita que seu programa viola a seguran√ßa da mem√≥ria, voc√™ tem uma vantagem
encontrando a causa: olhe para os blocos `unsafe` (e como eles interagem com
c√≥digo circundante).

* Nota: Este √© um trabalho em andamento.

### Structs com campos de refer√™ncia

Structs com refer√™ncias requerem definir
explicitamente o valor inicial para um
valor de refer√™ncia, a menos que a struct
j√° defina seu pr√≥prio valor inicial.

Refer√™ncias de valor zero, ou ponteiros nulos,
** N√ÉO ** ser√£o compat√≠veis no futuro,
por enquanto, structs de dados como Linked Lists
ou √Årvores Bin√°rias que dependem de campos de refer√™ncia
que podem usar o valor `0`, entendendo que n√£o √© seguro, e que pode
causar p√¢nico.

```v
struct Node {
	a &Node
	b &Node = 0 // Auto-initialized to nil, use with caution!
}

// Reference fields must be initialized unless an initial value is declared.
// Zero (0) is OK but use with caution, it's a nil pointer.
foo := Node{
	a: 0
}
bar := Node{
	a: &foo
}
baz := Node{
	a: 0
	b: 0
}
qux := Node{
	a: &foo
	b: &bar
}
println(baz)
println(qux)
```

## sizeof and __offsetof

* `sizeof(Type)` d√° o tamanho de um tipo em bytes.
* `__offsetof(Struct, field_name)` fornece o
deslocamento em bytes de um campo de estrutura.

```v
struct Foo {
	a int
	b int
}

assert sizeof(Foo) == 8
assert __offsetof(Foo, a) == 0
assert __offsetof(Foo, b) == 4
```

## Chamando C de V

### Exemplo

```v
#flag -lsqlite3
#include "sqlite3.h"
// See also the example from https://www.sqlite.org/quickstart.html
struct C.sqlite3 {
}

struct C.sqlite3_stmt {
}

type FnSqlite3Callback = fn (voidptr, int, &&char, &&char) int

fn C.sqlite3_open(&char, &&C.sqlite3) int

fn C.sqlite3_close(&C.sqlite3) int

fn C.sqlite3_column_int(stmt &C.sqlite3_stmt, n int) int

// ... voc√™ tamb√©m pode apenas definir o tipo de par√¢metro e omitir o prefixo C.
fn C.sqlite3_prepare_v2(&C.sqlite3, &char, int, &&C.sqlite3_stmt, &&char) int

fn C.sqlite3_step(&C.sqlite3_stmt)

fn C.sqlite3_finalize(&C.sqlite3_stmt)

fn C.sqlite3_exec(db &C.sqlite3, sql &char, cb FnSqlite3Callback, cb_arg voidptr, emsg &&char) int

fn C.sqlite3_free(voidptr)

fn my_callback(arg voidptr, howmany int, cvalues &&char, cnames &&char) int {
	unsafe {
		for i in 0 .. howmany {
			print('| ${cstring_to_vstring(cnames[i])}: ${cstring_to_vstring(cvalues[i]):20} ')
		}
	}
	println('|')
	return 0
}

fn main() {
	db := &C.sqlite3(0) // this means `sqlite3* db = 0`
	// passar uma string literal para uma chamada de fun√ß√£o C resulta em uma string C, n√£o uma string V
	C.sqlite3_open(c'users.db', &db)
	// C.sqlite3_open(db_path.str, &db)
	query := 'select count(*) from users'
	stmt := &C.sqlite3_stmt(0)
	// Obs: voc√™ tamb√©m pode usar o campo `.str` de uma string V,
	// para obter sua representa√ß√£o terminada em zero no estilo C
	C.sqlite3_prepare_v2(db, &char(query.str), -1, &stmt, 0)
	C.sqlite3_step(stmt)
	nr_users := C.sqlite3_column_int(stmt, 0)
	C.sqlite3_finalize(stmt)
	println('There are $nr_users users in the database.')
	//
	error_msg := &char(0)
	query_all_users := 'select * from users'
	rc := C.sqlite3_exec(db, &char(query_all_users.str), my_callback, voidptr(7), &error_msg)
	if rc != C.SQLITE_OK {
		eprintln(unsafe { cstring_to_vstring(error_msg) })
		C.sqlite3_free(error_msg)
	}
	C.sqlite3_close(db)
}
```

### Passando sinalizadores de compila√ß√£o C

Adicione as diretivas `#flag` ao topo de seus
arquivos V para fornecer sinalizadores de compila√ß√£o C como:

- `-I` para adicionar caminhos de pesquisa de arquivos de inclus√£o C
- `-l` para adicionar nomes de bibliotecas C que voc√™ deseja vincular
- `-L` para adicionar caminhos de pesquisa de arquivos de biblioteca C
- `-D` para definir vari√°veis ‚Äã‚Äãde tempo de compila√ß√£o

Voc√™ pode (opcionalmente) usar sinalizadores diferentes para alvos diferentes.
Atualmente os sinalizadores `linux`,` darwin`, `freebsd` e` windows` s√£o suportados.

Obs: Cada flag deve ir em sua pr√≥pria linha (por enquanto)

```v oksyntax
#flag linux -lsdl2
#flag linux -Ivig
#flag linux -DCIMGUI_DEFINE_ENUMS_AND_STRUCTS=1
#flag linux -DIMGUI_DISABLE_OBSOLETE_FUNCTIONS=1
#flag linux -DIMGUI_IMPL_API=
```

No comando de constru√ß√£o do console, voc√™ pode usar:
* `-cflags` para passar sinalizadores personalizados para o compilador C de backend.
* `-cc` para alterar o compilador backend C padr√£o.
* Por exemplo: `-cc gcc-9 -cflags -fsanitize = thread`.

Voc√™ pode definir uma vari√°vel de ambiente `VFLAGS` em seu terminal para armazenar seu` -cc`
e configura√ß√µes `-cflags`, ao inv√©s de inclu√≠-los no comando de constru√ß√£o todas as vezes.

### #pkgconfig

Adicionar a diretiva `#pkgconfig` √© usada para dizer
ao compilador quais m√≥dulos devem ser usados ‚Äã‚Äãpara compilar
e vinculando usando os arquivos pkg-config fornecidos
pelas respectivas depend√™ncias.

Contanto que crases n√£o possam ser usados ‚Äã‚Äãem `#flag` e
processos de spawning n√£o sejam desej√°veis ‚Äã‚Äãpara seguran√ßa
e por motivos de portabilidade,
V usa sua pr√≥pria biblioteca pkgconfig que √© compat√≠vel com o padr√£o
freedesktop one.

Se nenhuma sinaliza√ß√£o for passada,
ele adicionar√° `--cflags` e` --libs`, ambas as linhas abaixo fazem o mesmo:

```v oksyntax
#pkgconfig r_core
#pkgconfig --cflags --libs r_core
```

Os arquivos `.pc` s√£o pesquisados ‚Äã‚Äãem
uma lista codificada de caminhos padr√£o do pkg-config, o usu√°rio pode adicionar
caminhos extras usando a vari√°vel de
ambiente `PKG_CONFIG_PATH`. V√°rios m√≥dulos podem ser passados.

### Incluindo c√≥digo C

Voc√™ tamb√©m pode incluir o c√≥digo C
diretamente em seu m√≥dulo V.
Por exemplo, digamos que seu c√≥digo C
esteja localizado em uma pasta chamada 'c' dentro da pasta do m√≥dulo.
Ent√£o:

* Coloque um arquivo v.mod dentro da
pasta de n√≠vel superior do seu m√≥dulo (se voc√™
criou seu m√≥dulo com `v new` voc√™ j√°
tem o arquivo v.mod). Por
exemplo:
```v ignore
Module {
	name: 'mymodule',
	description: 'My nice module wraps a simple C library.',
	version: '0.0.1'
	dependencies: []
}
```


* Adicione estas linhas ao topo do seu m√≥dulo:
`` `v oksyntax
#flag -I @ VMODROOT / c
#flag @ VMODROOT / c / implementa√ß√£o.o
#include "header.h"
`` `
Obs: @VMODROOT ser√° substitu√≠do por 
V com a * pasta pai mais pr√≥xima,
onde existe um arquivo v.mod *.
Qualquer arquivo .v ao lado ou abaixo
da pasta onde o arquivo v.mod est√°,
pode usar `#flag @ VMODROOT/abc` para
se referir a esta pasta.
A pasta @VMODROOT tamb√©m √© * anexada *
ao caminho de pesquisa do m√≥dulo,
assim voc√™ pode * importar * outros m√≥dulos
em seu @VMODROOT, apenas nomeando-os.

As instru√ß√µes acima far√£o com que
V procure um arquivo .o compilado em
seu m√≥dulo `pasta/c/implementation.o`.
Se V o encontrar, o arquivo 
ser√° vinculado ao execut√°vel principal, que usou o m√≥dulo.
Se n√£o o encontrar, V assume que existe um arquivo `@ VMODROOT/c/implementation.c`,
e tenta compil√°-lo em um arquivo .o, ent√£o o usar√°.

Isso permite que voc√™ tenha um c√≥digo C,
que est√° contido em um m√≥dulo V, para que sua distribui√ß√£o seja mais f√°cil.
Voc√™ pode ver um exemplo m√≠nimo completo
para usar o c√≥digo C em um m√≥dulo V wrapper aqui:
[project_with_c_code](https://github.com/vlang/v/tree/master/vlib/v/tests/project_with_c_code).
Outro exemplo, demonstrando a passagem de
estruturas de C para V e vice-versa:
[interoperar entre C para V para C](https://github.com/vlang/v/tree/master/vlib/v/tests/project_with_c_code_2).

### Tipos C

Strings C terminadas em zero comuns podem
ser convertidas em strings V com
`unsafe {& char(cstring) .vstring()}` ou se voc√™ j√° sabe o comprimento com
`inseguro {& char(cstring) .vstring_with_len(len)}`.

Obs: Os m√©todos .vstring() e
.vstring_with_len() N√ÉO criam uma c√≥pia do `cstring`,
ent√£o voc√™ N√ÉO deve liber√°-lo ap√≥s chamar o m√©todo `.vstring()`.
Se voc√™ precisar fazer uma c√≥pia da string C
(algumas APIs libc como `getenv` praticamente exigem isso,
j√° que eles retornam ponteiros para a mem√≥ria
libc interna), voc√™ pode usar `cstring_to_vstring(cstring)`.

No Windows, as APIs C geralmente retornam
as chamadas strings `wide` (codifica√ß√£o utf16).
Eles podem ser convertidos em strings V com `string_from_wide(&u16(cwidestring))`.

V tem esses tipos para facilitar a
interoperabilidade com C:

- `voidptr` para` void * `do C,
- `& byte` para` byte * `do C e
- `& char` para` char * `do C.
- `&& char` para` char ** `do C

Para converter um `voidptr` para uma
refer√™ncia V, use` user := &User(user_void_ptr) `.

`voidptr` tamb√©m pode ser desreferenciado
em uma estrutura V atrav√©s da convers√£o:` user := User(user_void_ptr) `.

[um exemplo de um m√≥dulo que chama o c√≥digo C de V](https://github.com/vlang/v/blob/master/vlib/v/tests/project_with_c_code/mod1/wrapper.v)

### Declara√ß√µes C

Os identificadores C s√£o acessados ‚Äã‚Äãcom
o prefixo `C` da mesma forma que
identificadores s√£o acessados. As fun√ß√µes devem ser declaradas
novamente em V antes que possam ser usadas.
Qualquer tipo C pode ser usado atr√°s do prefixo `C`,
mas os tipos devem ser declarados novamente em V em
para acessar os membros do tipo.

Para redeclarar tipos complexos, como no seguinte c√≥digo C:

```c
struct SomeCStruct {
	uint8_t implTraits;
	uint16_t memPoolData;
	union {
		struct {
			void* data;
			size_t size;
		};

		DataView view;
	};
};
```

membros de subestruturas de dados podem ser declarados
diretamente na estrutura de conten√ß√£o conforme abaixo:

```v
struct C.SomeCStruct {
	implTraits  byte
	memPoolData u16
	// Esses membros fazem parte de subestruturas de dados que atualmente n√£o podem ser representadas em V.
	// Declar√°-los diretamente dessa forma √© suficiente para o acesso.
	// union {
	// struct {
	data voidptr
	size size_t
	// }
	view C.DataView
	// }
}
```

A exist√™ncia dos membros de dados √© informada a V,
e eles podem ser usados ‚Äã‚Äãsem
recriando exatamente a estrutura original.

Alternativamente, voc√™ pode [embed](#embedded-structs) as subestruturas de dados para manter
uma estrutura de c√≥digo paralela.

## Debugando de c√≥digo C gerado

Para depurar problemas no c√≥digo C gerado, voc√™ pode passar estes sinalizadores:

- `-g` - produz um execut√°vel menos otimizado com mais informa√ß√µes de depura√ß√£o nele.
    V ir√° for√ßar os n√∫meros de linha dos arquivos .v nos stacktraces, que o
    execut√°vel produzir√° em 'panic'. Geralmente √© melhor passar -g, a menos que
    voc√™ est√° escrevendo c√≥digo de baixo n√≠vel, caso em que use a pr√≥xima op√ß√£o `-cg`.
- `-cg` - produz um execut√°vel menos otimizado com mais informa√ß√µes de depura√ß√£o nele.
	O execut√°vel usar√° n√∫meros de linha de origem C neste caso. √â frequente
    usado em combina√ß√£o com `-keepc`, para que voc√™ possa inspecionar o
    Programa C em caso de p√¢nico, ou para que o seu depurador (`gdb`,` lldb` etc.)
    pode mostrar o c√≥digo-fonte C gerado.
- `-showcc` - imprime o comando C que √© usado para construir o programa.
- `-show-c-output` - imprime a sa√≠da que seu compilador C produziu
    enquanto compila seu programa.
- `-keepc` - n√£o exclui o arquivo de c√≥digo-fonte C gerado ap√≥s um
    compila√ß√£o. Al√©m disso, continue usando o mesmo caminho de arquivo,
	para que seja mais est√°vel,
    e mais f√°cil de manter aberto em um editor / IDE.

Para obter a melhor experi√™ncia de depura√ß√£o,
se voc√™ estiver escrevendo um wrapper de baixo n√≠vel para um existente
Biblioteca C, voc√™ pode passar v√°rios desses sinalizadores ao mesmo tempo:
`v -keepc -cg -showcc yourprogram.v`, ent√£o apenas execute o seu depurador (gdb/lldb) ou IDE
no execut√°vel produzido `your program`.

Se voc√™ quiser apenas inspecionar o c√≥digo C gerado,
sem mais compila√ß√£o, voc√™ tamb√©m pode usar o sinalizador `-o` (por exemplo,` -o file.c`).
Isso far√° com que V produza o `file.c` e ent√£o pare.

Se voc√™ quiser ver o c√≥digo-fonte C gerado para * apenas * uma √∫nica fun√ß√£o C,
por exemplo `main`, voc√™ pode usar:` -printfn main -o file.c`.

Para ver uma lista detalhada de todos os sinalizadores que V suporta,
use `v help`,` v help build` e `v help build-c`.

## Compila√ß√£o Condicional

### Compile time code

`$` √© usado como um prefixo para opera√ß√µes de tempo de compila√ß√£o.

#### $if
```v
// Suporte para v√°rias condi√ß√µes
$if ios || android {
	println('Running on a mobile device!')
}
$if linux && x64 {
	println('64-bit Linux.')
}
// Usado como express√£o
os := $if windows { 'Windows' } $else { 'UNIX' }
println('Using $os')
// $else-$if branches
$if tinyc {
	println('tinyc')
} $else $if clang {
	println('clang')
} $else $if gcc {
	println('gcc')
} $else {
	println('different compiler')
}
$if test {
	println('testing')
}
// v -cg ...
$if debug {
	println('debugging')
}
// v -prod ...
$if prod {
	println('production build')
}
// v -d option ...
$if option ? {
	println('custom option')
}
```

Se voc√™ deseja que um `if` seja avaliado em tempo de compila√ß√£o,
ele deve ser prefixado com um sinal `$`.
No momento, ele pode ser usado para detectar um sistema operacional,
compilador, plataforma ou op√ß√µes de compila√ß√£o.
`$if debug` √© uma op√ß√£o especial como `$if windows` ou `$if x32`.
Se estiver usando um ifdef personalizado, voc√™ precisa da op√ß√£o `$if?{}`
e compilar com a op√ß√£o `v -d`.
Lista completa de op√ß√µes integradas:
| OS                            | Compilers         | Platforms             | Other                     |
| ---                           | ---               | ---                   | ---                       |
| `windows`, `linux`, `macos`   | `gcc`, `tinyc`    | `amd64`, `arm64`      | `debug`, `prod`, `test`   |
| `mac`, `darwin`, `ios`,       | `clang`, `mingw`  | `x64`, `x32`          | `js`, `glibc`, `prealloc` |
| `android`,`mach`, `dragonfly` | `msvc`            | `little_endian`       | `no_bounds_checking`, `freestanding`    |
| `gnu`, `hpux`, `haiku`, `qnx` | `cplusplus`       | `big_endian`          |
| `solaris` | | | |

#### $embed_file

```v ignore
import os
fn main() {
	embedded_file := $embed_file('v.png')
	os.write_file('exported.png', embedded_file.to_string()) ?
}
```

V pode embutir arquivos arbitr√°rios no execut√°vel com o `$ embed_file(<path>)`
chamada de tempo de compila√ß√£o. Os caminhos podem ser absolutos
ou relativos ao arquivo de origem.

Quando voc√™ n√£o usa `-prod`, o arquivo n√£o ser√° incorporado. Em vez disso,
ser√° carregado * pela primeira vez * que seu programa chama `f.data()`
em tempo de execu√ß√£o, tornando
mais f√°cil mudar em programas de editor externo, sem a necessidade de recompilar
seu execut√°vel.

Quando voc√™ compila com `-prod`, o arquivo * ser√° embutido dentro * de seu
execut√°vel, aumentando seu tamanho bin√°rio, mas tornando-o mais independente
e, portanto, mais f√°cil de distribuir. Neste caso, `f.data()` causar√° * nenhum IO *,
e sempre retornar√° os mesmos dados.

#### $tmpl para incorporar e analisar arquivos de modelo V

V tem uma linguagem de modelo simples para modelos de texto e html,
e eles podem facilmente
ser embutido via `$tmpl('path/to/template.txt')`:


```v ignore
fn build() string {
	name := 'Peter'
	age := 25
	numbers := [1, 2, 3]
	return $tmpl('1.txt')
}

fn main() {
	println(build())
}
```

1.txt:

```
name: @name

age: @age

numbers: @numbers

@for number in numbers
  @number
@end
```

output:

```
name: Peter

age: 25

numbers: [1, 2, 3]

1
2
3
```




#### $env

```v
module main

fn main() {
	compile_time_env := $env('ENV_VAR')
	println(compile_time_env)
}
```

V pode trazer valores em tempo de compila√ß√£o a partir de vari√°veis ‚Äã‚Äãde ambiente.
`$env('ENV_VAR')` tamb√©m pode ser usado no n√≠vel superior `#flag` e `#include`:
`#flag linux -I $env('JAVA_HOME')/include`.

### Arquivos espec√≠ficos do ambiente

Se um arquivo tiver um sufixo espec√≠fico do ambiente,
ele ser√° compilado apenas para esse ambiente.

- `.js.v` => ser√° usado apenas pelo back-end JS. Esses arquivos podem conter c√≥digo JS.
- `.c.v` => ser√° usado apenas pelo back-end C. Esses arquivos podem conter c√≥digo C.
- `.native.v` => ser√° usado apenas pelo back-end nativo de V.
- `_nix.c.v` => ser√° usado apenas em sistemas Unix (n√£o Windows).
- `_${os}.c.v` => ser√° usado apenas no sistema `os` espec√≠fico.
Por exemplo, `_windows.c.v` ser√° usado apenas ao compilar no Windows, ou com `-os windows`.
- `_default.c.v` => ser√° usado apenas se N√ÉO houver um arquivo de plataforma mais espec√≠fico.
Por exemplo, se voc√™ tiver `file_linux.c.v` e `file_default.c.v`,
e voc√™ est√° compilando para Linux, ent√£o somente `file_linux.c.v` ser√° usado
e `file_default.c.v` ser√° ignorado.

Exemplo mais completo:
main.v:
```v ignore
module main
fn main() { println(message) }
```

main_default.c.v:
```v ignore
module main
const ( message = 'Hello world' )
```

main_linux.c.v:
```v ignore
module main
const ( message = 'Hello linux' )
```

main_windows.c.v:
```v ignore
module main
const ( message = 'Hello windows' )
```

Com o exemplo acima:
- ao compilar para o Windows, voc√™ obter√° 'Hello windows'
- quando voc√™ compila para o Linux, voc√™ obter√° 'Hello linux'
- ao compilar para qualquer outra plataforma, voc√™ obter√° o
mensagem n√£o espec√≠fica 'Hello, world'.

- `_d_customflag.v` => ser√° usado * apenas * se voc√™ passar` -d customflag` para V.
Isso corresponde a `$if customflag?{} `, mas para um arquivo inteiro, n√£o apenas um
bloco √∫nico. `customflag` deve ser um identificador snake_case, n√£o pode
conter caracteres arbitr√°rios (apenas letras latinas min√∫sculas + n√∫meros + `_`).
NB: um postfix combinat√≥rio `_d_customflag_linux.c.v` n√£o funcionar√°.
Se voc√™ precisar de um arquivo de sinalizador personalizado,
que tenha c√≥digo dependente da plataforma, use o
postfix `_d_customflag.v`, e ent√£o use o tempo de compila√ß√£o dependente do plaftorm
blocos condicionais dentro dele, ou seja, `$if linux{}` etc.

- `_notd_customflag.v` => semelhante a _d_customflag.v, mas ser√° usado
* apenas * se voc√™ N√ÉO passar `-d customflag` para V.

## Pseudo vari√°veis ‚Äã‚Äãde tempo de compila√ß√£o

V tamb√©m d√° ao seu c√≥digo acesso a um conjunto de vari√°veis ‚Äã‚Äãde pseudo string,
que s√£o substitu√≠dos em tempo de compila√ß√£o:

- `@FN` => substitu√≠do pelo nome da fun√ß√£o V atual
- `@METHOD` => substitu√≠do com ReceiverType.MethodName
- `@MOD` => substitu√≠do com o nome do atual m√≥dulo V
- `@STRUCT` => substitu√≠do com o nome do atual struct V
- `@FILE` => substitu√≠do com o caminho do arquivo de origem V
- `@LINE` => substitu√≠do com o n√∫mero da linha V onde aparece (como uma string).
- `@COLUMN` => substitu√≠do com a coluna onde aparece (como uma string).
- `@VEXE` => substitu√≠do com o caminho para o compilador V
- `@VEXEROOT`  => ser√° substitu√≠do pela * pasta *,
   onde est√° o execut√°vel V (como uma string).
- `@VHASH`  => substitu√≠do com o hash de commit encurtado do compilador V (como uma string).
- `@VMOD_FILE` => substitu√≠do com o conte√∫do do arquivo v.mod mais pr√≥ximo (como uma string).
- `@VMODROOT` => ser√° substitu√≠do pela * pasta *,
   onde est√° o arquivo v.mod mais pr√≥ximo (como uma string).

Isso permite que voc√™ fa√ßa o seguinte exemplo, √∫til ao depurar/registrar/rastrear seu c√≥digo:
```v
eprintln('file: ' + @FILE + ' | line: ' + @LINE + ' | fn: ' + @MOD + '.' + @FN)
```

Outro exemplo, √© se voc√™ deseja incorporar a vers√£o/nome de v.mod * dentro * de seu execut√°vel:
```v ignore
import v.vmod
vm := vmod.decode( @VMOD_FILE ) or { panic(err.msg) }
eprintln('$vm.name $vm.version\n $vm.description')
```

## Performance tuning

O c√≥digo C gerado, geralmente, √© r√°pido o suficiente, quando voc√™ compila seu c√≥digo
com `-prod`.
Existem algumas situa√ß√µes, por√©m, em que voc√™ pode querer dar
dicas adicionais para o compilador, para que ele possa otimizar ainda mais alguns
blocos de c√≥digo.

Obs: Estes s√£o * raramente * necess√°rios e n√£o devem ser usados, a menos que voc√™
* crie o perfil de seu c√≥digo * e veja se h√° benef√≠cios significativos para eles.
Para citar a documenta√ß√£o do gcc: "os programadores s√£o notoriamente ruins em prever
como seus programas realmente funcionam ".

`[inline]` - voc√™ pode marcar fun√ß√µes com `[inline]`, ent√£o o compilador C ir√°
tentar embuti-los, o que, em alguns casos, pode ser ben√©fico para o desempenho,
mas pode afetar o tamanho do seu execut√°vel.

`[direct_array_access]` - em fun√ß√µes marcadas com `[direct_array_access]`
o compilador ir√° traduzir as opera√ß√µes de array diretamente em opera√ß√µes de array C -
omitindo a verifica√ß√£o de limites.
Isso pode economizar muito tempo em uma fun√ß√£o que itera
sobre um array, mas ao custo de tornar a fun√ß√£o insegura - a menos que
os limites ser√£o verificados pelo usu√°rio.

`if _likely_(bool expression) {` isso sugere que o compilador C, que a
express√£o booleana √© muito prov√°vel que seja verdadeira, ent√£o pode gerar assembly
c√≥digo, com menos chance de erro de predi√ß√£o do branch. No back-end JS,
isso n√£o faz nada.

`if _unlikely_(bool expression) {` semelhante a `_likely_(x)`, mas sugere que
a express√£o booleana √© altamente improv√°vel. No back-end JS, isso n√£o faz nada.

<a id='Reflection via codegen'>

## Compile-time reflection

Ter suporte JSON integrado √© bom, mas V tamb√©m permite que voc√™ crie
serializadores para qualquer formato de dados.
V tem constru√ß√µes `if` e` for` em tempo de compila√ß√£o:

```v wip
// TODO: n√£o totalmente implementado

struct User {
    name string
    age  int
}

// Note: T deve receber apenas um nome de struct
fn decode<T>(data string) T {
    mut result := T{}
    // compile-time `for` loop
    // T.fields fornece um array de um tipo de metadados de campo
    $for field in T.fields {
        $if field.typ is string {
            // $(string_expr) produz um identificador
            result.$(field.name) = get_string(data, field.name)
        } $else $if field.typ is int {
            result.$(field.name) = get_int(data, field.name)
        }
    }
    return result
}

// `decode<User>` gera:
fn decode_User(data string) User {
    mut result := User{}
    result.name = get_string(data, 'name')
    result.age = get_int(data, 'age')
    return result
}
```

## Limited operator overloading

```v
struct Vec {
	x int
	y int
}

fn (a Vec) str() string {
	return '{$a.x, $a.y}'
}

fn (a Vec) + (b Vec) Vec {
	return Vec{a.x + b.x, a.y + b.y}
}

fn (a Vec) - (b Vec) Vec {
	return Vec{a.x - b.x, a.y - b.y}
}

fn main() {
	a := Vec{2, 3}
	b := Vec{4, 5}
	mut c := Vec{1, 2}
	println(a + b) // "{6, 8}"
	println(a - b) // "{-2, -2}"
	c += a
	println(c) // "{3, 5}"
}
```

A sobrecarga do operador vai contra a filosofia de simplicidade e previsibilidade de V.
Mas como as aplica√ß√µes cient√≠ficas e gr√°ficas est√£o entre os dom√≠nios de V,
a sobrecarga do operador √© um recurso importante para melhorar a legibilidade:

`a.add(b).add(c.mul(d))` √© muito menos leg√≠vel do que `a + b + c * d`.

Para melhorar a seguran√ßa e facilidade de manuten√ß√£o, a sobrecarga do operador √© limitada:

- S√≥ √© poss√≠vel sobrecarregar os operadores `+, -, *, /, %, <, >, ==, !=, <=, >=`.
- `==` e `!=` s√£o gerados automaticamente pelo compilador, mas podem ser sobrescritos.
- Chamar outras fun√ß√µes dentro das fun√ß√µes do operador n√£o √© permitido.
- As fun√ß√µes do operador n√£o podem modificar seus argumentos.
- Ao usar os operadores `<` e `==`, o tipo de retorno deve ser `bool`.
- `!=`, `>`, `<=` e `>=` s√£o gerados automaticamente quando `==` e `<` s√£o definidos.
- Ambos os argumentos devem ter o mesmo tipo (assim como todos os operadores em V).
- Operadores de atribui√ß√£o (`*=`, `+=`, `/=`, etc)
s√£o gerados automaticamente quando os operadores s√£o definidos,
embora devam retornar o mesmo tipo.

## Inline assembly
<!-- ignore because it doesn't pass fmt test (why?) -->
```v ignore
a := 100
b := 20
mut c := 0
asm amd64 {
    mov eax, a
    add eax, b
    mov c, eax
    ; =r (c) as c // output
    ; r (a) as a // input
      r (b) as b
}
println('a: $a') // 100
println('b: $b') // 20
println('c: $c') // 120
```

Para mais exemplos, veja [asm_test.amd64.v](https://github.com/vlang/v/tree/master/vlib/v/tests/assembly/asm_test.amd64.v)

## Traduzindo C para V

TODO: a tradu√ß√£o de C para V estar√° dispon√≠vel em V 0.3.

V pode traduzir seu c√≥digo C para c√≥digo
V leg√≠vel por humanos e gerar wrapper V em cima de bibliotecas C.


Vamos criar um programa simples `test.c` primeiro:

```c
#include "stdio.h"

int main() {
	for (int i = 0; i < 10; i++) {
		printf("hello world\n");
	}
        return 0;
}
```

Execute `v translate test.c`, e V gerar√° `test.v`:

```v
fn main() {
	for i := 0; i < 10; i++ {
		println('hello world')
	}
}
```

Para gerar um wrapper em cima de uma biblioteca C, use este comando:

```bash
v wrapper c_code/libsodium/src/libsodium
```

Isso ir√° gerar um diret√≥rio `libsodium` com um m√≥dulo V.

Exemplo de um wrapper libsodium gerado por C2V:

https://github.com/medvednikov/libsodium

<br>

Quando voc√™ deve traduzir o c√≥digo C e quando deve simplesmente chamar o c√≥digo C de V?

Se voc√™ tiver um c√≥digo C bem escrito e testado,
ent√£o, √© claro, voc√™ pode simplesmente chamar este c√≥digo C de V.

Traduzir para V oferece v√°rias vantagens:

- Se voc√™ planeja desenvolver essa base de c√≥digo, agora voc√™ tem tudo em uma linguagem,
    que √© muito mais seguro e f√°cil de desenvolver do que em C.
- A compila√ß√£o cruzada torna-se muito mais f√°cil. Voc√™ n√£o precisa se preocupar com isso.
- N√£o h√° mais sinalizadores de constru√ß√£o e arquivos de inclus√£o tamb√©m.

## Hot code reloading

```v live
module main

import time
import os

[live]
fn print_message() {
	println('Hello! Modify this message while the program is running.')
}

fn main() {
	for {
		print_message()
		time.sleep(500 * time.millisecond)
	}
}
```

Construa este exemplo com `v -live message.v`.

As fun√ß√µes que voc√™ deseja recarregar devem ter o atributo `[live]`
antes de sua defini√ß√£o.

No momento, n√£o √© poss√≠vel modificar os tipos durante a execu√ß√£o do programa.

Mais exemplos, incluindo um aplicativo gr√°fico:
[github.com/vlang/v/tree/master/examples/hot_code_reload](https://github.com/vlang/v/tree/master/examples/hot_reload).

## Cross compilation

Para compilar o seu projeto, basta executar

```shell
v -os windows .
```

ou

```shell
v -os linux .
```

(Cross compiling para macOS, temporariamente, n√£o √© poss√≠vel.)

Se voc√™ n√£o tiver nenhuma depend√™ncia C,
isso √© tudo que voc√™ precisa fazer. Isso funciona at√©
ao compilar aplicativos GUI usando o m√≥dulo `ui` ou aplicativos gr√°ficos usando` gg`.

Voc√™ precisar√° instalar o Clang, LLD linker e baixar um arquivo zip com
bibliotecas e incluem arquivos para Windows e Linux. V ir√° fornecer-lhe um link.

## Cross-platform shell scripts em V

V pode ser usado como uma alternativa ao Bash para escrever
scripts de implanta√ß√£o, construir scripts, etc.

A vantagem de usar V para isso √© a simplicidade e previsibilidade da linguagem, e
suporte multiplataforma. Os "scripts V"
s√£o executados em sistemas do tipo Unix e tamb√©m no Windows.

Use a extens√£o de arquivo `.vsh`. Isso far√° com que todas as fun√ß√µes no `os`
m√≥dulo global (para que voc√™ possa usar `mkdir()` ao inv√©s de `os.mkdir()`, por exemplo).

Um exemplo de `deploy.vsh`:
```v wip
#!/usr/bin/env -S v run
// O texto acima associa o arquivo V em sistemas semelhantes ao Unix,
// para que possa ser executado apenas especificando o caminho para o arquivo
// uma vez que √© tornado execut√°vel usando `chmod + x`.

// Remova se compilar/sair, ignore quaisquer erros se n√£o
rmdir_all('build') or { }

// Criar build/, nunca falha porque build/ n√£o existe
mkdir('build') ?

// Move arquivos *.v para build/
result := exec('mv *.v build/') ?
if result.exit_code != 0 {
	println(result.output)
}
// Similar a:
// files := ls('.') ?
// mut count := 0
// if files.len > 0 {
//     for file in files {
//         if file.ends_with('.v') {
//              mv(file, 'build/') or {
//                  println('err: $err')
//                  return
//              }
//         }
//         count++
//     }
// }
// if count == 0 {
//     println('No files')
// }
```

Agora voc√™ pode compilar isso como um programa V normal e
obter um execut√°vel que voc√™ pode implantar e executar
em qualquer lugar:
`v deploy.vsh && ./deploy`

Ou apenas execute-o mais como um script Bash tradicional:
`v execute deploy.vsh`

Em plataformas do tipo Unix, o arquivo pode ser executado
diretamente ap√≥s torn√°-lo execut√°vel usando `chmod + x`:
`./deploy.vsh`

## Attributes

V possui v√°rios atributos que modificam o comportamento de fun√ß√µes e structs.

Um atributo √© uma instru√ß√£o do compilador especificada dentro de `[]` logo antes de um
declara√ß√£o function/struct/enum e se aplica somente √† declara√ß√£o a seguir.

```v
// Chamar esta fun√ß√£o resultar√° em um aviso de suspens√£o de uso
[deprecated]
fn old_function() {
}

// Ele tamb√©m pode exibir uma mensagem de suspens√£o de uso personalizada
[deprecated: 'use new_function() instead']
fn legacy_function() {}

// As chamadas desta fun√ß√£o ser√£o sequenciais.
[inline]
fn inlined_function() {
}

// A struct a seguir deve ser alocada no heap. Portanto, ele s√≥ pode ser usado como uma
// refer√™ncia (`&Window`) ou dentro de outra refer√™ncia (`&OuterStruct{Window{...}}`).
[heap]
struct Window {
}

// V n√£o ir√° gerar esta fun√ß√£o e todas as suas chamadas se o sinalizador fornecido for falso.
// Para usar um sinalizador, use `v -d flag`
[if debug]
fn foo() {
}

fn bar() {
	foo() // n√£o ser√° chamado se `-d debug` n√£o for passado
}

// A mem√≥ria apontada pelos argumentos do ponteiro desta fun√ß√£o n√£o ser√°
// liberada pelo GC (se em uso) antes que a fun√ß√£o retorne
[keep_args_alive]
fn C.my_external_function(voidptr, int, voidptr) int

// As chamadas para a fun√ß√£o a seguir devem ser em blocos unsafe{}.
// Observe que o c√≥digo no corpo de `risky_business()` ainda ser√°
// verificado, a menos que voc√™ tamb√©m o envolva em blocos `unsafe{}`.
// Isso √© √∫til, quando voc√™ quer ter uma fun√ß√£o `[unsafe]` que
// tem verifica√ß√µes antes/depois de uma certa opera√ß√£o insegura, que ainda
// beneficie-se dos recursos de seguran√ßa do V.
[unsafe]
fn risky_business() {
	// c√≥digo que ser√° verificado, talvez verificando as pr√©-condi√ß√µes
	unsafe {
		// c√≥digo que * n√£o ser√° * verificado, como aritm√©tica de ponteiro,
		// acessando campos de uni√£o, chamando outros fns `[unsafe], etc ...
		// Normalmente, √© uma boa ideia tentar minimizar o c√≥digo empacotado
		// unsafe{} tanto quanto poss√≠vel.
		// Veja tamb√©m [Memory-unsafe code](#memory-unsafe-code)
	}
	// c√≥digo que ser√° verificado, talvez verificando as condi√ß√µes de postagem e/ou
	// mantendo invariantes
}

// O mecanismo autofree de V n√£o cuidar√° do gerenciamento de mem√≥ria nesta fun√ß√£o.
// Voc√™ ter√° a responsabilidade de liberar mem√≥ria manualmente nele.
[manualfree]
fn custom_allocations() {
}

// Somente para interoperabilidade C, diz a V que a struct a seguir
// √© definida com `struct typedef` em C
[typedef]
struct C.Foo {
}

// Usado no c√≥digo da API Win32 quando voc√™ precisa passar a fun√ß√£o de retorno de chamada
[windows_stdcall]
fn C.DefWindowProc(hwnd int, msg int, lparam int, wparam int)

// Apenas Windows:
// Se uma biblioteca de gr√°ficos padr√£o √© importada (ex. Gg, ui), a janela gr√°fica leva
// prioridade e nenhuma janela de console √© criada, efetivamente desabilitando as instru√ß√µes println().
// Use para criar explicitamente a janela do console. V√°lido apenas antes de main().
[console]
fn main() {
}
```

## Goto

V permite pular incondicionalmente para um r√≥tulo com `goto`. O nome do r√≥tulo deve estar contido
dentro da mesma fun√ß√£o da instru√ß√£o `goto`. Um programa pode `goto` um r√≥tulo fora
ou mais profundo do que o escopo atual. `goto` permite pular a inicializa√ß√£o da vari√°vel ou
voltar ao c√≥digo que acessa a mem√≥ria que j√° foi liberada, portanto, requer
`unsafe`.

```v ignore
if x {
	// ...
	if y {
		unsafe {
			goto my_label
		}
	}
	// ...
}
my_label:
```
`goto` deve ser evitado, particularmente quando `for` pode ser usado em seu lugar.
[Labelled break/continue](#labelled-break--continue) pode ser usado para escapar de
um loop aninhado, e aqueles n√£o correm o risco de violar a seguran√ßa da mem√≥ria.

# Ap√™ndices

## Ap√™ndice I: Keywords

V tem 41 keywords reservadas (3 s√£o literais):

```v ignore
as
asm
assert
atomic
break
const
continue
defer
else
embed
enum
false
fn
for
go
goto
if
import
in
interface
is
lock
match
module
mut
none
or
pub
return
rlock
select
shared
sizeof
static
struct
true
type
typeof
union
unsafe
__offsetof
```
Veja tamb√©m [Tipos](#tipos).

## Ap√™ndice II: Operators

lista de operators para [tipos primitivos](#tipos-primitivos) somente.

```v ignore
+    sum                    integers, floats, strings
-    difference             integers, floats
*    product                integers, floats
/    quotient               integers, floats
%    remainder              integers

~    bitwise NOT            integers
&    bitwise AND            integers
|    bitwise OR             integers
^    bitwise XOR            integers

!    logical NOT            bools
&&   logical AND            bools
||   logical OR             bools
!=   logical XOR            bools

<<   left shift             integer << unsigned integer
>>   right shift            integer >> unsigned integer


Preced√™ncia    Operador
    5             *  /  %  <<  >>  &
    4             +  -  |  ^
    3             ==  !=  <  <=  >  >=
    2             &&
    1             ||


Assignment Operators
+=   -=   *=   /=   %=
&=   |=   ^=
>>=  <<=
```
